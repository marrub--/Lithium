#library "lithdbob"
#include "zcommon.acs"

function int lerp(int a, int b, int t)
{
   int ret = FixedMul((1.0 - t), a) + FixedMul(t, b);
   
   if(((ret >> 4) << 4) == b)
      return b;
   
   return ret;
}

script "Lith_DamageBobOnly" enter
{
   int health;
   int prevhealth;
   
   int addangle;
   int addpitch;
   
   while(true)
   {
      prevhealth = health;
      health = GetActorProperty(0, APROP_Health);
      
      if(health < prevhealth)
      {
         int mul = GetCVar("lith_player_damagebobmul");
         
         int distance;
         int angle;
         
         angle = Random(0.0, 1.0);

         distance = FixedDiv((prevhealth - health) << 16, 100.0);
         distance = FixedDiv(distance, 7.0);

         addangle = FixedMul(FixedMul(Sin(angle), distance), mul);
         addpitch = FixedMul(FixedMul(Cos(angle), distance), mul);
      }

      addangle = lerp(addangle, 0.0, 0.1);
      addpitch = lerp(addpitch, 0.0, 0.1);

      SetActorAngle(0, GetActorAngle(0) - addangle);
      SetActorPitch(0, GetActorPitch(0) - addpitch);

      Delay(1);

      SetActorAngle(0, GetActorAngle(0) + addangle);
      SetActorPitch(0, GetActorPitch(0) + addpitch);
   }
}

// EOF

