/*-*-Agw-LithDlg-*-*/
nimi abyss;

jo {
   prog_tutorial = prog_dialogue,
   prog_m,
   prog_c,
   prog_i,
   prog_w,
   prog_a,
   prog_d,
   prog_t,
   var_lane = var_beg,
   var_jem  = var_lane $10 +,
   var_fulk = var_jem  $10 +,
   var_luke = var_fulk $10 +,
   var_omi  = var_luke $10 +,
   var_ari  = var_omi  $10 +,
   var_kiri = var_ari  $10 +,
}

/* Tutorial */
ilo_kon(prog_tutorial) {
   lipu(page_start) {
      name   unknown;
      remote data;
      icon   data;
      text   begin;
      option begin_nxt jump_page 1;
      option begin_esc brk;
   }
   lipu(1) {
      text question;
      option question_esc brk;
   }
}

/* Marine */
ilo_kon(prog_m) {
   jo {
      lane_met = var_lane,
      lane_rpg,
      lane_where,
      lane_scars,
      m_meet = 1,
      m_nothing,
      m_seen,
      mc_meet,
      mc_reminded,
      mc_real,
      mc_others,
      mc_where,
      mc_fine,
      mc_1649,
      mc_time,
      mc_experience,
      mc_coverup,
      mo_meet,
      mo_past,
   }
   lipu(page_start) {
      name   doll;
      remote lane;
      icon   doll;
      text   m_begin_0;
      seme !insa lane_met {
         option m_begin_o0 {
            seme     poki_jan pM jump_page m_meet;
            anu seme poki_jan pC jump_page mc_meet;
            anu                  jump_page mo_meet;
         }
      }
      option m_begin_o1 brk;
   }
   /* Lane */
   lipu(m_meet) {
      text m_meet_0;
      option m_meet_o0 jump_page m_nothing;
      option m_meet_o1 brk;
      option m_meet_o2 jump_page m_seen;
   }
   lipu(m_nothing) {
      seme !insa lane_rpg
         option m_nothing_o0 {inc %lane_rpg;   jump_page m_nothing;}
      seme !insa lane_where
         option m_nothing_o1 {inc %lane_where; jump_page m_nothing;}
      option m_nothing_o2 {
         inc %lane_met;
         jump_page m_seen;
      }
      option m_nothing_o3 brk;
      seme insa lane_rpg {
         text m_nothing_1;
         dec %lane_rpg;
      } anu seme insa lane_where {
         text m_nothing_2;
         dec %lane_where;
      } anu {
         text m_nothing_0;
      }
   }
   lipu(m_seen) {
      seme insa lane_scars {
         text m_seen_1;
         script "Lith_SetLane";
         option m_seen_o2 brk;
         option m_seen_o3 brk;
      } anu {
         text m_seen_0;
         option m_seen_o0 brk;
         option m_seen_o1 {
            inc %lane_scars;
            jump_page m_seen;
         }
      }
   }
   /* Jem */
   lipu(mc_meet) {
      text mc_meet_0;
      option mc_meet_o0 jump_page mc_reminded;
      option mc_meet_o1 jump_page mc_real;
   }
   lipu(mc_reminded) {
      text mc_reminded_0;
      option mc_reminded_o0 jump_page mc_others;
      option mc_reminded_o1 jump_page mc_real;
   }
   lipu(mc_real) {text mc_real_0; option mc_real_o0 jump_page mc_1649;}
   lipu(mc_others) {
      text mc_others_0;
      option mc_others_o0 jump_page mc_where;
      option mc_others_o1 brk;
   }
   lipu(mc_where) {text mc_where_0; option mc_where_o0 jump_page mc_fine;}
   lipu(mc_fine)  {text mc_fine_0;  option mc_fine_o0  jump_page mc_1649;}
   lipu(mc_1649) {
      inc %lane_met;
      text mc_1649_0;
      option mc_1649_o0 jump_page mc_time;
   }
   lipu(mc_time) {text mc_time_0; option mc_time_o0 jump_page mc_experience;}
   lipu(mc_experience) {
      script "Lith_SetLane";
      text mc_experience_0;
      option mc_experience_o0 jump_page mc_coverup;
   }
   lipu(mc_coverup) {
      text mc_coverup_0;
      option mc_coverup_o0 brk;
      option mc_coverup_o1 brk;
   }
   /* Other */
   lipu(mo_meet) {
      inc %lane_met;
      text mo_meet_0;
      option mo_meet_o0 brk;
      option mo_meet_o1 jump_page mo_past;
      option mo_meet_o2 brk;
   }
   lipu(mo_past) {
      script "Lith_SetLane";
      text mo_past_0;
      option mo_past_o0 brk;
   }
}

/* Cyber-Mage */
ilo_kon(prog_c) {
   jo {
      jem_met = var_jem,
      c_meet = 1,
         c_crying,
            c_notokay,
               c_dontstop,
                  c_notreal,
                     c_alive,
                     c_howknow,
                        c_newgame,
                        c_startanew,
            c_front,
               c_whycry,
                  c_mycase,
                     c_dismantle,
      co_meet,
   }
   lipu(page_start) {
      name   doll;
      remote jem;
      icon   doll;
      text   c_begin_0;
      option c_begin_o0 brk;
      seme !insa jem_met {
         option c_begin_o1 {
            seme poki_jan pC jump_page c_meet;
            anu              jump_page co_meet;
         }
      }
   }
   /* Jem */
   lipu(c_meet) {
      inc %jem_met;
      text c_meet_0;
      option c_meet_o0 jump_page c_crying;
   }
   lipu(c_crying) {
      text c_crying_0;
      option c_crying_o0 jump_page c_notokay;
      option c_crying_o1 jump_page c_front;
   }
   lipu(c_notokay) {
      text c_notokay_0;
      option c_notokay_o0 brk;
      option c_notokay_o1 jump_page c_dontstop;
   }
   lipu(c_dontstop) {
      text c_dontstop_0;
      option c_dontstop_o0 jump_page c_notreal;
   }
   lipu(c_notreal) {
      text c_notreal_0;
      option c_notreal_o0 jump_page c_alive;
      option c_notreal_o1 jump_page c_howknow;
      option c_notreal_o2 brk;
   }
   lipu(c_alive) {
      text c_alive_0;
      option c_alive_o0 brk;
      option c_alive_o1 jump_page c_newgame;
   }
   lipu(c_howknow) {
      text c_howknow_0;
      option c_howknow_o0 jump_page c_newgame;
      option c_howknow_o1 brk;
   }
   lipu(c_newgame) {
      text c_newgame_0;
      option c_newgame_o0 jump_page c_startanew;
   }
   lipu(c_startanew) {
      text c_startanew_0;
      option c_startanew_o0 brk;
   }
   lipu(c_front) {
      text c_front_0;
      option c_front_o0 jump_page c_whycry;
   }
   lipu(c_whycry) {
      text c_whycry_0;
      option c_whycry_o0 jump_page c_mycase;
      option c_whycry_o1 brk;
   }
   lipu(c_mycase) {
      text c_mycase_0;
      option c_mycase_o0 brk;
      option c_mycase_o1 jump_page c_dismantle;
   }
   lipu(c_dismantle) {
      text c_dismantle_0;
      option c_dismantle_o0 brk;
   }
   /* Other */
   lipu(co_meet) {
      inc %jem_met;
      text co_meet_0;
      option co_meet_o0 brk;
      option co_meet_o1 brk;
   }
}

/* Informant */
ilo_kon(prog_i) {
   lipu(page_start) {
      name   doll;
      remote fulk;
      icon   doll;
      text   t_begin_0;
      option t_begin_o0 brk;
   }
}

/* Wanderer */
ilo_kon(prog_w) {
   lipu(page_start) {
      name   doll;
      remote luke;
      icon   doll;
      text   t_begin_0;
      option t_begin_o0 brk;
   }
}

/* Assassin */
ilo_kon(prog_a) {
   lipu(page_start) {
      name   doll;
      remote omi;
      icon   doll;
      text   t_begin_0;
      option t_begin_o0 brk;
   }
}

/* Dark Lord */
ilo_kon(prog_d) {
   jo {
      ari_met = var_ari,
      ari_doesitmatter,
      ari_death,
      ari_right,
      ari_sister,
      d_meet = 1,
      d_mirror,
      d_whym,
      d_selse,
      d_mindseye,
      d_right,
      d_unintuit,
      d_holdys,
      d_doll,
      d_hallow,
      do_meet,
   }
   lipu(page_start) {
      name   doll;
      remote ari;
      icon   doll;
      text   d_begin_0;
      option d_begin_o0 brk;
      seme !insa ari_met {
         option d_begin_o1 {
            seme poki_jan pD jump_page d_meet;
            anu              jump_page do_meet;
         }
      }
   }
   /* Ari */
   lipu(d_meet) {
      inc %ari_met;
      text d_meet_0;
      option d_meet_o0 jump_page d_mirror;
      option d_meet_o1 jump_page d_selse;
      option d_meet_o2 brk;
   }
   lipu(d_mirror) {
      text d_mirror_0;
      seme !insa ari_doesitmatter {
         option d_mirror_o0 jump_page d_whym;
      }
      option d_mirror_o1 jump_page d_doll;
      option d_mirror_o2 jump_page d_unintuit;
   }
   lipu(d_whym) {
      seme !insa ari_doesitmatter {
         text d_whym_0;
         option d_whym_o0 {
            inc %ari_doesitmatter;
            jump_page d_whym;
         }
         option d_whym_o1 jump_page d_mirror;
      } anu {
         text d_whym_1;
         option d_whym_o2 jump_page d_unintuit;
      }
   }
   lipu(d_selse) {
      text d_selse_0;
      option d_selse_o0 jump_page d_mindseye;
      option d_selse_o1 jump_page d_right;
      option d_selse_o2 jump_page d_unintuit;
   }
   lipu(d_mindseye) {
      text d_mindseye_0;
      option d_mindseye_o0 jump_page d_doll;
      option d_mindseye_o1 brk;
   }
   lipu(d_right) {
      text d_right_0;
      option d_right_o0 jump_page d_doll;
   }
   lipu(d_unintuit) {
      text d_unintuit_0;
      option d_unintuit_o0 jump_page d_holdys;
      option d_unintuit_o1 {inc %ari_death; jump_page d_holdys;}
      option d_unintuit_o2 jump_page d_doll;
   }
   lipu(d_holdys) {
      seme !insa ari_death {text d_holdys_0;} anu {text d_holdys_1;}
      text2 d_holdys_2;
      option d_holdys_o0 jump_page d_hallow;
   }
   lipu(d_doll) {
      text d_doll_0;
      seme !insa ari_right {
         option d_doll_o0 {inc %ari_right; jump_page d_doll;}
      } anu {
         option d_doll_o2 jump_page d_doll;
      }
      option d_doll_o1 {inc %ari_sister; jump_page d_hallow;}
   }
   lipu(d_hallow) {
      text d_hallow_0;
      seme insa ari_sister {text2 d_hallow_2;} anu {text2 d_hallow_1;}
      option d_hallow_o0 brk;
   }
   /* Other */
   lipu(do_meet) {
      inc %ari_met;
      text do_meet_0;
      option do_meet_o0 brk;
   }
}

/* Thoth */
ilo_kon(prog_t) {
   jo {
      kiri_met = var_kiri,
      kiri_self,
      t_breaking = 1,
      t_down,
      t_broken,
      t_entity,
   }
   lipu(page_start) {
      name   doll;
      remote kiri;
      icon   doll;
      option t_begin_o0 brk;
      seme poki_jan pT {
         seme !insa kiri_met {
            text t_begin_2;
            option t_begin_o1 jump_page t_breaking;
         } anu {
            seme insa kiri_self {
               text t_begin_1;
            } anu {
               text t_begin_0;
            }
         }
      } anu {
         text t_begin_0;
      }
   }
   /* Kiri */
   lipu(t_breaking) {
      inc %kiri_met;
      text t_breaking_0;
      text2 t_begin_2;
      option t_breaking_o0 jump_page t_down;
   }
   lipu(t_down) {
      text t_down_0;
      option t_down_o0 jump_page t_broken;
      option t_down_o1 brk;
   }
   lipu(t_broken) {
      inc %kiri_self;
      text t_broken_0;
      option t_broken_o0 jump_page t_entity;
   }
   lipu(t_entity) {
      text t_entity_0;
      option t_entity_o0 brk;
   }
}

/* EOF */
