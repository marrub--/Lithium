actor Lith_WeaponGlow
{
   RenderStyle "Add"
   Alpha 0.7
   Scale 0.1
   Translation "0:255=%[0,0,0]:[0,0.5,1]"
   
   +NOINTERACTION
   
   states
   {
   Spawn:
      PBAL A 5 bright
      PBAL A 1 bright A_FadeOut(1.0 / 35.0)
      wait
   }
}

actor Lith_WeaponSprite
{
   +NOINTERACTION
   
   var int user_origx;
   var int user_origy;
   var int user_origz;
   var int user_floatangle;
   var int user_floatzangle;
   
   states
   {
   Spawn:
      "####" "#" 0 A_SetUserVar(user_floatangle,  (user_floatangle  + 2) % 360)
      "####" "#" 0 A_SetUserVar(user_floatzangle, (user_floatzangle + 3) % 360)
      "####" "#" 0 A_SetUserVar(user_origx, x * FIX)
      "####" "#" 0 A_SetUserVar(user_origy, y * FIX)
      "####" "#" 0 A_SetUserVar(user_origz, z * FIX)
      "####" "#" 1 bright A_Warp(AAPTR_MASTER, cos(user_floatangle) * 4.0,
                                               sin(user_floatangle) * 4.0,
                                               16.0 + (sin(user_floatzangle) * 12.0),
                                 0.0, WARPF_NOCHECKPOSITION)
      "####" "#" 0 bright A_Warp(AAPTR_MASTER, user_origx / FIX,
                                               user_origy / FIX,
                                               user_origz / FIX,
                                 0, WARPF_ABSOLUTEPOSITION)
      loop
   }
}

actor Lith_Weapon : Weapon
{
   Weapon.BobStyle "Alpha"
   Weapon.BobSpeed 1.5
   Inventory.PickupSound ""
   Inventory.PickupMessage ""
   
   +INVENTORY.UNTOSSABLE // This is actually handled correctly but unfortunately fuck you.
   +WEAPON.NOALERT
   +NOTIMEFREEZE // This is needed so you can pick up weapons while time is frozen.
   
   var int user_glowangle;
   var int user_glowzangle;
   var int user_pickupparm;
   var int user_tid;
   
   const int LITH_BOBONLY = WRF_NOSWITCH | WRF_NOFIRE;
   
   states
   {
   MaybeWeShouldPickUpAnyways:
      TNT1 A 0 A_JumpIf(CallACS("Lith_PickupScore", user_pickupparm, user_tid), "SpawnLoopNoReallySeriouslyForRealThisTime")
      TNT1 A 1
      stop
   SpawnLoop:
      "####" "#" 1
      "####" "#" 0 Thing_ChangeTID(0, CallACS("Lith_UniqueTID"))
      "####" "#" 0 A_SetUserVar("user_tid", CallACS("Lith_UniqueTID"))
      "####" "#" 0 A_SpawnItemEx("Lith_WeaponSprite", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERSPRITEFRAME | SXF_SETMASTER, 0, user_tid)
   SpawnLoopForRealYo:
      // 226:ACS_ExecuteAlways
      TNT1 A 0 A_SetSpecial(226, 14242, 0, user_pickupparm, user_tid)
      TNT1 A 1 A_CheckSight("LoopButDontShowThatShitOffDawg")
      TNT1 A 0 A_LookEx(LOF_NOSOUNDCHECK | LOF_DONTCHASEGOAL | LOF_NOJUMP, 0, 256, 0, 360)
      TNT1 A 0 A_JumpIfCloser(64, "MaybeWeShouldPickUpAnyways")
   SpawnLoopNoReallySeriouslyForRealThisTime:
      TNT1 A 0 A_SetUserVar(user_glowangle,  (user_glowangle  + 5) % 360)
      TNT1 A 0 A_SetUserVar(user_glowzangle, (user_glowzangle + 2) % 360)
      
      // xyz
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", sin(user_glowangle) * 32.0,
                                                cos(user_glowangle) * 32.0,
                                                24.0 + (sin(user_glowzangle) * 9.0),
                                                0, 0, 0, 0)
      
      // yz
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", 0,
                                                sin(user_glowangle) * 32.0,
                                                24.0 + (cos(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", cos(user_glowangle) * 32.0,
                                                sin(user_glowangle) * 32.0,
                                                24.0 + (sin(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", cos(user_glowangle) * 32.0,
                                                ((1.0 - sin(user_glowangle)) * 32.0) - 32.0,
                                                24.0 + (sin(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      
      // xz
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", sin(user_glowangle) * 32.0,
                                                0,
                                                24.0 + (cos(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", sin(user_glowangle) * 32.0,
                                                cos(user_glowangle) * 32.0,
                                                24.0 + (sin(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      TNT1 A 0 A_SpawnItemEx("Lith_WeaponGlow", ((1.0 - sin(user_glowangle)) * 32.0) - 32.0,
                                                cos(user_glowangle) * 32.0,
                                                24.0 + (sin(user_glowangle) * 32.0),
                                                0, 0, 0, 0)
      goto SpawnLoopForRealYo
   LoopButDontShowThatShitOffDawg:
      TNT1 A 1
      TNT1 A 0 A_CheckSight("LoopButDontShowThatShitOffDawg")
      goto SpawnLoopForRealYo
   
   DummyFlash:
      TNT1 A 0 A_AlertMonsters
      stop
   
   User4:
      "####" "#" 3 offset(2, 34)
      "####" "#" 1 offset(5, 41)
      "####" "#" 1 offset(10, 58)
      "####" "#" 1 offset(17, 87)
      TNT1 A 0 offset(1, 32) A_JumpIfInventory("Lith_Knif", 1, "_Knife1")
      KNIF A 1 A_PlaySound("weapons/knife/swing1", CHAN_WEAPON)
      KNIF BCD 1
      goto _KnifeNormal
   _Knife1:
      TNT1 A 1 A_PlaySound("weapons/knife/swing2", CHAN_WEAPON)
      KNIF FGH 1
   _KnifeNormal:
      TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "_KnifeBerserk")
      TNT1 A 0 A_CustomPunch(5  * random(1, 10), true, CPF_NOTURN, "Lith_KnifPuf", 96)
      goto _KnifeEnd1
   _KnifeBerserk:
      TNT1 A 0 A_CustomPunch(20 * random(1, 10), true, CPF_NOTURN, "Lith_KnifPuf", 96)
      goto _KnifeEnd1
   _KnifeEnd1:
      TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "_KnifeEnd2")
      KNIF E 1
      TNT1 A 1
      goto _KnifeEnd
   _KnifeEnd2:
      KNIF IJ 1
   _KnifeEnd:
      TNT1 A 5
      TNT1 A 0 A_JumpIfInventory("Lith_Knif", 1, "_KnifeDone")
      TNT1 A 0 A_GiveInventory("Lith_Knif", 1)
      TNT1 AAAAA 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH|WRF_ALLOWUSER4)
      TNT1 A 0 A_TakeInventory("Lith_Knif", 1)
      goto _KnifeFinish
   _KnifeDone:
      TNT1 A 0 A_TakeInventory("Lith_Knif", 1)
      TNT1 AAAAA 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH|WRF_ALLOWUSER4)
   _KnifeFinish:
      TNT1 AAAAAAAAA 0 A_Lower
      TNT1 A 1
      TNT1 A 0 A_Jump(256, "Select")
   }
}

