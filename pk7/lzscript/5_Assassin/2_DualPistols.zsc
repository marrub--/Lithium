/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_DualPistols : Lith_AWeapon;

default {
   Lith_Weapon.SpawnType "Lith_PistolPickup";
   Lith_Weapon.MagSize 20;
   Tag "$LITH_INFO_SHORT_DualPistols";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/dualpistols/draw";
}

action(Weapon) void Lith_A_FireDualPistols(int xofs) {
   Lith_A_MagIncr();
   A_GunFlash("DummyFlash");
   Lith_FireBullets(2, 1, -1, 10, tracerX: xofs);
   Lith_A_GunSmokeSmall(xofs * 0.4);
   Lith_A_RecoilUp(0.4);
   A_FireProjectile("Lith_LightProjectileRedShort", 0);
}

states(Weapon) {
Ready:   TNT1 A 1 Lith_A_Ready(WRF_ALLOWRELOAD); wait;
ReadyLH: DPIS B 1 Lith_A_DoubleReady;            wait;
ReadyRH: DPIS A 1 Lith_A_DoubleReady;            wait;

Deselect:
   TNT1 A 1 A_Lower(12);
   loop;

Select:
   TNT1 A 0 Lith_A_DoubleRaise;
   TNT1 A 1 A_Raise(12);
   wait;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty;
   TNT1 A 5 Lith_A_DoubleState(_oscillate, "FireLH", "FireRH");
   TNT1 AA 1 A_ReFire;
   goto ReadySelector;
FireLH:
   DPIS B 0 A_StartSound("weapons/dualpistols/firel", lch_weapon);
   DPIS E 1 bright {
      A_OverlayOffset(_psp_double_lg, FRandom(0.1, 0.5) * -15.0, FRandom(0, 5), WOF_INTERPOLATE);
      Lith_A_FireDualPistols(-10);
   }
   DPIS F 1 bright A_OverlayOffset(_psp_double_lg, -4, 2, WOF_ADD|WOF_INTERPOLATE);
   DPIS J 1 bright A_OverlayOffset(_psp_double_lg, -2, 1, WOF_ADD|WOF_INTERPOLATE);
   DPIS H 1 bright A_OverlayOffset(_psp_double_lg, -2, 1, WOF_ADD|WOF_INTERPOLATE);
   DPIS B 0 A_OverlayOffset(_psp_double_lg, 0, 0, WOF_INTERPOLATE);
   goto ReadyLH;
FireRH:
   DPIS A 0 A_StartSound("weapons/dualpistols/firer", lch_weapon2);
   DPIS C 1 bright {
      A_OverlayOffset(_psp_double_rg, FRandom(0.1, 0.5) * 15.0, FRandom(0, 5), WOF_INTERPOLATE);
      Lith_A_FireDualPistols(10);
   }
   DPIS D 1 bright A_OverlayOffset(_psp_double_rg, 4, 2, WOF_ADD|WOF_INTERPOLATE);
   DPIS I 1 bright A_OverlayOffset(_psp_double_rg, 2, 1, WOF_ADD|WOF_INTERPOLATE);
   DPIS G 1 bright A_OverlayOffset(_psp_double_rg, 2, 1, WOF_ADD|WOF_INTERPOLATE);
   DPIS A 0 A_OverlayOffset(_psp_double_rg, 0, 0, WOF_INTERPOLATE);
   goto ReadyRH;
FireEmptyLH:
   DPIS H 2 Lith_A_DoubleOffset(true,  0, 9);
   DPIS B 1 Lith_A_DoubleOffset(true, -1, 13);
   #### # 1 Lith_A_DoubleOffset(true, -2, 11);
   #### # 1 Lith_A_DoubleOffset(true, -2, 8);
   #### # 1 Lith_A_DoubleOffset(true, -2, 6);
   #### # 1 Lith_A_DoubleOffset(true, -1, 4);
   #### # 1 Lith_A_DoubleOffset(true,  0, 1);
   #### # 1 Lith_A_DoubleOffset(true,  0, 0);
   goto ReadyLH;
FireEmptyRH:
   DPIS G 2 Lith_A_DoubleOffset(false, 0, 9);
   DPIS A 1 Lith_A_DoubleOffset(false, 1, 13);
   #### # 1 Lith_A_DoubleOffset(false, 2, 11);
   #### # 1 Lith_A_DoubleOffset(false, 2, 8);
   #### # 1 Lith_A_DoubleOffset(false, 2, 6);
   #### # 1 Lith_A_DoubleOffset(false, 1, 4);
   #### # 1 Lith_A_DoubleOffset(false, 0, 1);
   #### # 1 Lith_A_DoubleOffset(false, 0, 0);
   goto ReadyRH;
FireEmpty:
   #### # 6 Lith_A_DoubleState(_oscillate, "FireEmptyLH", "FireEmptyRH");
   #### # 9 A_StartSound("weapons/cshotgun/empty", lch_dryweapon);
   #### #### 1 A_ReFire;
   goto ReadySelector;

ReloadLH:
   DPIS B 1  Lith_A_DoubleOffset(_lhs,  0, 2);
   #### # 0  Lith_A_DropMag("Lith_PistolMagazine", "Lith_PistolMagazine2", -10,0,20, FRandom(1,3),0,0);
   #### # 0  A_StartSound("weapons/dualpistols/out", lch_reload);
   #### # 1  Lith_A_DoubleOffset(_lhs, -1, 4);
   #### # 1  Lith_A_DoubleOffset(_lhs, -2, 13);
   #### # 1  Lith_A_DoubleOffset(_lhs, -3, 23);
   #### # 1  Lith_A_DoubleOffset(_lhs, -4, 43);
   #### # 5  Lith_A_DoubleOffset(_lhs, -4, 110);
   #### # 10 A_StartSound("weapons/dualpistols/in", lch_reload);
   #### # 1  Lith_A_DoubleOffset(_lhs, -4, 43);
   #### # 1  Lith_A_DoubleOffset(_lhs, -3, 23);
   #### # 1  Lith_A_DoubleOffset(_lhs, -2, 13);
   #### # 1  Lith_A_DoubleOffset(_lhs, -1, 4);
   #### # 1  Lith_A_DoubleOffset(_lhs,  0, 2);
   #### # 1  Lith_A_DoubleOffset(_lhs,  0, 0);
   goto ReadyLH;
ReloadRH:
   DPIS A 3  Lith_A_DoubleOffset(_rhs, 0, 2);
   #### # 0  Lith_A_DropMag("Lith_PistolMagazine", "Lith_PistolMagazine2", 10,0,20, FRandom(1,3),0,0);
   #### # 0  A_StartSound("weapons/dualpistols/out", lch_reload2, pitch: 0.75);
   #### # 1  Lith_A_DoubleOffset(_rhs, 1, 4);
   #### # 1  Lith_A_DoubleOffset(_rhs, 2, 13);
   #### # 1  Lith_A_DoubleOffset(_rhs, 3, 23);
   #### # 1  Lith_A_DoubleOffset(_rhs, 4, 43);
   #### # 5  Lith_A_DoubleOffset(_rhs, 4, 110);
   #### # 10 A_StartSound("weapons/dualpistols/in", lch_reload2, pitch: 0.75);
   #### # 1  Lith_A_DoubleOffset(_rhs, 4, 43);
   #### # 1  Lith_A_DoubleOffset(_rhs, 3, 23);
   #### # 1  Lith_A_DoubleOffset(_rhs, 2, 13);
   #### # 1  Lith_A_DoubleOffset(_rhs, 1, 4);
   #### # 1  Lith_A_DoubleOffset(_rhs, 0, 2);
   #### # 1  Lith_A_DoubleOffset(_rhs, 0, 0);
   goto ReadyRH;
Reload:
   #### # 0  Lith_A_JumpIfMagFull;
   #### # 5  Lith_A_DoubleState(_no_oscill, "ReloadLH", "ReloadRH");
   #### # 10 Lith_A_ReloadBobDn(0.4);
   #### # 10 Lith_A_ReloadBobUp(0.2);
   #### # 0  A_StartSound("weapons/dualpistols/slide", lch_reload3);
   #### # 10 Lith_A_ReloadFinish;
   goto ReadySelector;
}

/* EOF */
