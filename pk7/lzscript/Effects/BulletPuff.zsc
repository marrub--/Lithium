// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │               By Alison G. Watson, Ryan Cordell and Xaser.               │
// │             Attribution is encouraged, though not required.              │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_PuffSmoke : Actor {
   default {
      Radius 1;
      Height 1;
      RenderStyle "Add";
      Alpha 0.3;
      Scale 0.4;
      +NoInteraction
      +FloorClip
      +ForceXyBillboard
      +DontSplash
   }
   states {
   Spawn:
      SMK5 ABCDEFGHIJKLMNOP 1;
      stop;
   }
}

class Lith_HotEmber : Actor {
   default {
      Radius 2;
      Height 2;
      Mass 8;
      Gravity 0.45;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +Bright
      +NoBlockmap
      +DontSplash
      -NoGravity
   }
   states {
   Spawn:
      LI0A A 1 light("Lith_BulletPuff1");
      LI0A BBCC 4 light("Lith_BulletPuff2") a_spawnItemEx("Lith_HotEmberTrail", fRandom[lith_bulletpuff](0, 0.5), fRandom[lith_bulletpuff](-0.5, 0.5), fRandom[lith_bulletpuff](-0.5, 0.5), fRandom[lith_bulletpuff](0, 0.5), fRandom[lith_bulletpuff](-0.5, 0.5), fRandom[lith_bulletpuff](-0.5, 0.5));
      LI0A DEFG 4 light("Lith_BulletPuff3");
      stop;
   }
}

class Lith_HotEmberTrail : Actor {
   default {
      Radius 2;
      Height 2;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +DontSplash
      +NoInteraction
   }
   states {
   Spawn:
      EPUF EFG 5 light("Lith_BulletPuff3");
      stop;
   }
}
class Lith_BulletAfterSmoke : Actor {
   default {
      RenderStyle "Add";
      Alpha 0.01;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;
      Projectile;
      +ThruActors
      +ForceXyBillboard
      +DontSplash
   }
   double m_sx, m_sy;
   override void postBeginPlay() {
      super.postBeginPlay();
      scale.x *= fRandom[lith_bulletpuff](2, 6);
      scale.y *= fRandom[lith_bulletpuff](0.4, 1.5);
      m_sx = fRandom[lith_bulletpuff](0.005, 0.01);
      m_sy = fRandom[lith_bulletpuff](0.001, 0.005);
   }
   override void tick() {
      super.tick();
      if(!isFrozen()) {
         scale.x += m_sx;
         scale.y += m_sy;
      }
   }
   states {
   Spawn:
      TNT1 A 0;
      SMOK AAAAAAAAAABBBBBBBBBBCCCCCCCCCC 1 a_fadeIn(fRandom[lith_bulletpuff](0.001, 0.005));
      SMOK DEFG random(14, 56);
      SMOK HHHHHHHHHIIIIIIIIIJJJJJJJJJJJJJJKKKKKKKKK 1 a_fadeOut(0.005);
      stop;
   }
}

class Lith_BulletTracer : FastProjectile {
   default {
      Damage 0;
      Radius 2;
      Height 2;
      Speed 67;
      RenderStyle "Add";
      +Bright
      +NoDamage
      +DontSplash
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      int n = randomPick[lith_bulletpuff](45, -45);
      roll = fRandom[lith_bulletpuff](n - 2, n + 2);
   }

   states {
   Spawn:
      ____ A -1 light("Lith_BulletPuff1");
      stop;
   }
}

class Lith_BulletTracerSlow : Lith_BulletTracer {
   default {
      Speed 43;
   }
}

class Lith_BulletTracerBig : Lith_BulletTracerSlow {
}

class Lith_BulletRicochetSpark : Actor {
   default {
      RenderStyle "Add";
      +NoInteraction
      +DontSplash
      +FlatSprite
      +RollSprite
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      roll  = fRandom[lith_bulletpuff](0, 359);
      pitch = fRandom[lith_bulletpuff](-5, 5) + 45;
      double sc = fRandom[lith_bulletpuff](0.05, 0.2);
      scale = (sc, sc);
   }
   states {
   Spawn:
      TNT1 A 0;
      SPRK ABCD 0 a_jump(51, "Spawn1");
   Spawn1:
      #### # random(1, 2) bright light("Lith_BulletPuff1");
      stop;
   }
}

class Lith_BulletPuffPainless : Lith_BulletPuff {default {+PAINLESS}}
class Lith_BulletPuffPainful  : Lith_BulletPuff {default {+FORCEPAIN}}

class Lith_BulletPuff : Actor replaces BulletPuff;

default {
   RenderStyle "Add";
   DamageType "Bullet";
   Species "Lith_Player";
   Alpha 0.9;
   +Bright
   +DontHarmSpecies
   +NoGravity
   +NoBlockmap
   +FloorClip
   +ForceXyBillboard
   +PuffGetsOwner
   +MThruSpecies
   -AllowParticles
}
void lith_a_smokeFX() {
   for(int i = 0, m = random[lith_bulletpuff](0, 2); i < m; i++) {
      a_spawnItemEx("Lith_BulletAfterSmoke", fRandom[lith_bulletpuff](-1,1), fRandom[lith_bulletpuff](-1,1), fRandom[lith_bulletpuff](-2,2),
         fRandom[lith_bulletpuff](-0.5,0.5), fRandom[lith_bulletpuff](-0.5,0.5), fRandom[lith_bulletpuff](-0.2,0.2));
   }
}
void lith_a_ricochetFX() {
   if(random[lith_bulletpuff](0, 255) < 32) return;
   for(int i = 0, m = random[lith_bulletpuff](0, 3); i < m; i++) {
      a_spawnItemEx("Lith_HotEmber", fRandom[lith_bulletpuff](-1, 1), fRandom[lith_bulletpuff](-1, 1), fRandom[lith_bulletpuff](0, 1), fRandom[lith_bulletpuff](-4, 4), fRandom[lith_bulletpuff](-4, 4), fRandom[lith_bulletpuff](2, 5), 0, SXF_NOCHECKPOSITION);
   }
   a_spawnParticle("FFFFFF", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
      fRandom[lith_bulletpuff](-8,8), fRandom[lith_bulletpuff](-8,8), fRandom[lith_bulletpuff](-2,2),
      fRandom[lith_bulletpuff]( 0,1), fRandom[lith_bulletpuff](-5,5), fRandom[lith_bulletpuff]( 5,10),
      0, 0, fRandom[lith_bulletpuff](-0.5, -3),
      1.0, -1);
}
void lith_a_playRicochetSound() {
   if(random(0, 6) == 0) {
      a_startSound("effects/puff/ricochet", lch_body, volume: lith_weapons_ricochetvol);
   }
}
override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("effects/puff/ricochet");
   markSound("marathon/hit");
   markSound("marathon/hitwall");
   markSound("marathon/hitsolid");
}
override void postBeginPlay() {
   super.postBeginPlay();
   if(random[lith_bulletpuff](0, 3) == 0) {
      lith_a_smokeFX();
   }
   if(random[lith_bulletpuff](0, 2) == 0) {
      lith_a_ricochetFX();
   }
   if(Lith_UTIL.pData(_pdt_upgrade, UPGR_TorgueMode)) {
      a_spawnItemEx("Lith_EXPLOOOSION", flags: SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
   }
   if(random[lith_bulletpuff](0, 4) == 0) {
      a_spawnItemEx("Lith_BulletRicochetSpark");
   }
   if(random[lith_bulletpuff](0, 100) == 0) {
      a_spawnItemEx("Lith_PuffSmoke", 0, 0, 4.0 + 0.1 * random[lith_bulletpuff](-10, 10));
   }
}
states {
Spawn:
   XPUF A 0;
   XPUF A 0 a_jumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Seven7s), "SpawnMarathon");
Spawn1:
   XPUF A 0 a_jump(256, "PuffNormal", "PuffNormalAlt", "PuffMirrored", "PuffMirroredAlt");
   stop;
Crash:
   XPUF A 0;
   XPUF A 0 a_jumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Seven7s), "CrashMarathon");
   goto Spawn1;
PuffNormal:
   XPUF QR 1 light("Lith_BulletPuff1");
   XPUF STU 1 light("Lith_BulletPuff2");
   goto PuffNormalEnd;
PuffNormalAlt:
   XPUF A 0 lith_a_playRicochetSound;
   XPUF AB 1;
   XPUF CDE 1 light("Lith_BulletPuff2");
PuffNormalEnd:
   XPUF FG 1 light("Lith_BulletPuff3");
   XPUF H 1;
   stop;
PuffMirrored:
   XPUF VW 1 light("Lith_BulletPuff1");
   XPUF XYZ 1 light("Lith_BulletPuff2");
   goto PuffMirroredEnd;
PuffMirroredAlt:
   XPUF I 0 lith_a_playRicochetSound;
   XPUF IJ 1;
   XPUF KLM 1 light("Lith_BulletPuff2");
PuffMirroredEnd:
   XPUF NO 1 light("Lith_BulletPuff3");
   XPUF P 1;
   stop;
CrashMarathon:
   TNT1 A 0 a_startSound("marathon/hitwall", lch_body, volume: lith_weapons_ricochetvol);
   goto MarathonPuff;
SpawnMarathon:
   TNT1 A 0 a_startSound("marathon/hitsolid", lch_body, volume: lith_weapons_ricochetvol);
   goto MarathonPuff;
MarathonPuff:
   TNT1 A 0 A_SetScale(0.5);
   TNT1 A 0 A_SetTranslucent(1, 1);
   MPUF ABCDEFG 2 bright;
   stop;
}

/* EOF */
