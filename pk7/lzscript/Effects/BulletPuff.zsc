/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson, Ryan Cordell and Xaser. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */


class Lith_PuffSmoke : Actor
{
   default
   {
      Radius 1;
      Height 1;
      RenderStyle "Add";
      Alpha 0.3;
      Scale 0.4;

      +NOINTERACTION
      +FLOORCLIP
      +FORCEXYBILLBOARD
      +DONTSPLASH
   }

   states
   {
   Spawn:
      SMK5 ABCDEFGHIJKLMNOP 1;
      stop;
   }
}

class Lith_HotEmber : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Mass 8;
      Gravity 0.45;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +NOBLOCKMAP
      +DONTSPLASH
      -NOGRAVITY
   }

   states
   {
   Spawn:
      EPUF A 1 bright light("Lith_BulletPuff1");
      EPUF BBCC 4 bright light("Lith_BulletPuff2") A_SpawnItemEx("Lith_HotEmberTrail", FRandom(0, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5), FRandom(0, 0.5), FRandom(-0.5, 0.5), FRandom(-0.5, 0.5));
      EPUF DEFG 4 bright light("Lith_BulletPuff3");
      stop;
   }
}

class Lith_HotEmberTrail : Actor
{
   default
   {
      Radius 2;
      Height 2;
      Renderstyle "Translucent";
      Alpha 0.9;
      Scale 0.3;
      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      EPUF EFG 5 light("Lith_BulletPuff3");
      stop;
   }
}

class Lith_BulletAfterSmoke : Actor {
   default {
      RenderStyle "Add";
      Alpha 0.01;
      Speed 20;
      Radius 3;
      Height 3;
      Scale 0.3;

      Projectile;
      +THRUACTORS
      +FORCEXYBILLBOARD
      +DONTSPLASH
   }

   double m_sx, m_sy;

   override void postBeginPlay() {
      super.postBeginPlay();
      scale.x *= FRandom(2, 6);
      scale.y *= FRandom(0.4, 1.5);
      m_sx = FRandom(0.005, 0.01);
      m_sy = FRandom(0.001, 0.005);
   }

   override void tick() {
      super.tick();

      if(!isFrozen()) {
         scale.x += m_sx;
         scale.y += m_sy;
      }
   }

   states
   {
   Spawn:
      TNT1 A 0;
      SMOK AAAAAAAAAABBBBBBBBBBCCCCCCCCCC 1 A_FadeIn(FRandom(0.001, 0.005));
      SMOK DEFG random(14, 56);
      SMOK HHHHHHHHHIIIIIIIIIJJJJJJJJJJJJJJKKKKKKKKK 1 A_FadeOut(0.005);
      stop;
   }
}

class Lith_BulletTracer : FastProjectile
{
   default
   {
      Damage 0;
      Radius 2;
      Height 2;
      Speed 67;
      RenderStyle "Add";

      +NODAMAGE
      +DONTSPLASH
   }

   override void postBeginPlay()
   {
      super.postBeginPlay();
      int n = RandomPick(45, -45);
      Roll = FRandom(n - 2, n + 2);
   }

   states
   {
   Spawn:
      ____ A -1 bright light("Lith_BulletPuff1");
      stop;
   }
}

class Lith_BulletRicochetSpark : Actor
{
   default
   {
      RenderStyle "Add";

      +NOINTERACTION
      +DONTSPLASH
   }

   override void postBeginPlay()
   {
      super.postBeginPlay();
      Roll = FRandom(0, 359);
      Pitch = FRandom(-5, 5);
      double sc = FRandom(.5, 1);
      scale = (sc, sc);
   }

   states
   {
   Spawn:
      TNT1 A 0;
      ____ ABCD 0 A_Jump(51, "Spawn1");
   Spawn1:
      #### # random(1, 2) bright light("Lith_BulletPuff1");
      stop;
   }
}

class Lith_BulletPuffPainless : Lith_BulletPuff {default {+PAINLESS}}
class Lith_BulletPuffPainful  : Lith_BulletPuff {default {+FORCEPAIN}}

class Lith_BulletPuff : Actor replaces BulletPuff;

default
{
   RenderStyle "Add";
   DamageType "Bullet";
   Species "Lith_Player";
   Alpha 0.9;

   +DONTHARMSPECIES
   +NOGRAVITY
   +NOBLOCKMAP
   +FLOORCLIP
   +FORCEXYBILLBOARD
   +PUFFGETSOWNER
   +MTHRUSPECIES
   -ALLOWPARTICLES
}

void Lith_A_SmokeFX()
{
   for(int i = 0, m = random(0, 2); i < m; i++)
      A_SpawnItemEx("Lith_BulletAfterSmoke", FRandom(-1,1), FRandom(-1,1), FRandom(-2,2),
         FRandom(-0.5,0.5), FRandom(-0.5,0.5), FRandom(-0.2,0.2));
}

void Lith_A_RicochetFX()
{
   if(random() < 32) return;

   for(int i = 0, m = random(0, 3); i < m; i++)
      A_SpawnItemEx("Lith_HotEmber", FRandom(-1, 1), FRandom(-1, 1), FRandom(0, 1), FRandom(-4, 4), FRandom(-4, 4), FRandom(2, 5), 0, SXF_NOCHECKPOSITION);

   A_SpawnParticle("FFFFFF", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 4, 0,
      FRandom(-8,8), FRandom(-8,8), FRandom(-2,2),
      FRandom( 0,1), FRandom(-5,5), FRandom( 5,10),
      0, 0, FRandom(-0.5, -3),
      1.0, -1);
}

override void postBeginPlay()
{
   super.postBeginPlay();
   Lith_A_SmokeFX();
   Lith_A_RicochetFX();
   if(ACS_ExecuteWithResult(lsc_pdata, pdata_upgrade, UPGR_TorgueMode, true))
      A_SpawnItemEx("Lith_EXPLOOOSION", flags: SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
}

states
{
Spawn:
   XPUF Q 0;
   XPUF Q 0 A_SpawnItemEx("Lith_BulletRicochetSpark");
   XPUF Q 0 A_SpawnItemEx("Lith_PuffSmoke", 0, 0, 4.0 + 0.1 * random(-10, 10));
   XPUF Q 0 A_Jump(256, "PuffNormal", "PuffMirrored");
PuffNormal:
   XPUF Q 0 A_Jump(32, "PuffNormalAlt");
   XPUF QR 1 bright light("Lith_BulletPuff1");
   XPUF STU 1 bright light("Lith_BulletPuff2");
PuffNormalEnd:
   XPUF FG 1 bright light("Lith_BulletPuff3");
   XPUF H 1 bright;
   stop;
PuffNormalAlt:
   XPUF A 0 A_StartSound("effects/puff/ricochet", lch_body, volume: lith_weapons_ricochetvol);
   XPUF AB 1 bright;
   XPUF CDE 1 bright light("Lith_BulletPuff2");
   goto PuffNormalEnd;
PuffMirrored:
   XPUF V 0 A_Jump(32, "PuffMirroredAlt");
   XPUF VW 1 bright light("Lith_BulletPuff1");
   XPUF XYZ 1 bright light("Lith_BulletPuff2");
PuffMirroredEnd:
   XPUF NO 1 bright light("Lith_BulletPuff3");
   XPUF P 1 bright;
   stop;
PuffMirroredAlt:
   XPUF I 0 A_StartSound("effects/puff/ricochet", lch_body, volume: lith_weapons_ricochetvol);
   XPUF IJ 1 bright;
   XPUF KLM 1 bright light("Lith_BulletPuff2");
   goto PuffMirroredEnd;
}

/* EOF */
