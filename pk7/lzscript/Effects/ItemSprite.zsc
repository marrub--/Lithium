// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_FadeOutItemSprite : Lith_ItemSprite {
   states {
   Done:
      #### # 1 A_FadeOut;
      wait;
   }
}

class Lith_ItemSprite : Actor;

default {
   +NoInteraction
   +CastSpriteShadow
}

double m_fAng;
double m_fzAng;
State m_doneState;

override void postBeginPlay() {
   super.postBeginPlay();
   m_fAng  = fRandom(0, 359);
   m_fzAng = fRandom(0, 359);
   m_doneState = resolveState("Done");
}

override void tick() {
   if(isFrozen()) {
      return;
   }
   Lith_UTIL.stateTick(self);
   if(master && (master.bSpecial || master.special)) {
      let fAng  = level.time/35.0*66.66 + m_fAng;
      let fzAng = level.time/35.0*100.0 + m_fzAng;
      setOrigin(master.pos + (cos(fAng) * 4.0, sin(fAng) * 4.0, 16.0 + sin(fzAng) * 12.0), true);
   } else if(!inStateSequence(curState, m_doneState)) {
      setState(m_doneState);
   }
}

states {
Spawn:
   #### # -1 bright;
   stop;
Done:
   TNT1 A 0;
   stop;
}

/* EOF */
