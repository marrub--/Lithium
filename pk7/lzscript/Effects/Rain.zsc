// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │                       Effects taken from Realm667.                       │
// │   I think Tormenter made these originally, sorry if that's incorrect.    │
// │               Thanks to Kate for making these look better.               │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RainDrop : Actor {
   default {
      Alpha 0.6;
      RenderStyle "Add";
      Height 2;
      Radius 2;
      +FlatSprite
      +DontSplash
      +DropOff
      +NoBlockmap
      +NoInteraction
      +NoTeleport
      +NoTrigger
      +RollSprite
   }
   bool m_done;
   double m_rollSpeed;
   void lith_giveUp() {
      bRollSprite = false;
      bFlatSprite = false;
      setOrigin((pos.xy, floorZ), false);
      vel    = (0, 0, 0);
      roll   = 0;
      scale  = (1, 1);
      m_done = true;
      setState(resolveState("Death"));
   }
   override void tick() {
      if(!isFrozen()) {
         Lith_UTIL.stateTick(self);
         if(m_done) {
            return;
         }
         if(m_rollSpeed) {
            a_setRoll(roll + m_rollSpeed, SPF_INTERPOLATE);
         }
         if(waterLevel > 0 || pos.z <= floorZ) {
            lith_giveUp();
         } else {
            setOrigin(pos + vel, true);
         }
      }
   }
   states {
   Spawn:
      RNDR A 350;
      RNDR A 0 lith_giveUp;
      stop;
   Death:
      RNDR BCDEFGH 3;
      stop;
   }
}

class Lith_BloodRainDrop : Lith_RainDrop {
   default {
      Alpha 0.5;
      RenderStyle "Subtract";
      Translation "Lith_BloodRainDrop";
      +Bright
   }
}

class Lith_AbyssRainDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
   }
   states {
   Spawn:
      RNDZ A 350;
      RNDZ A 0 lith_giveUp;
      stop;
   Death:
      RNDZ BCDEFGH 3;
      stop;
   }
}

class Lith_SnowDrop : Lith_RainDrop {
   default {
      Alpha 1.0;
      RenderStyle "Normal";
   }
   override void postBeginPlay() {
      super.postBeginPlay();
      m_rollSpeed = fRandom[lith_rain](0.4, 0.9) * fRandomPick[lith_rain](-1, 1);
      angle = fRandom[lith_rain](0, 360);
      pitch = fRandom[lith_rain](0, 180);
      roll  = fRandom[lith_rain](0, 360);
      scale = (0.25, 0.25) * fRandom[lith_rain](1.0, 1.4);
      vel  *= fRandom[lith_rain](0.03, 0.1);
   }
   states {
   Spawn:
      RNDS A 350;
      RNDS A 0 lith_giveUp;
      stop;
   Death:
      RNDS B 0 a_setScale(0.5);
      RNDS BCDE 12;
      stop;
   }
}

class Lith_Ash : Lith_SnowDrop {
   default {
      RenderStyle "Translucent";
      Alpha 0.9;
   }
   states {
   Spawn:
      RNDA A 300;
      RNDA A 0 lith_giveUp;
      stop;
   Death:
      RNDA A 0 a_setScale(0.5);
      RNDA A 1 a_fadeOut(0.25 / 21.0);
      wait;
   }
}

/* EOF */
