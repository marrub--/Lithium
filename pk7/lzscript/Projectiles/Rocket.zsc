// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RocketTrail : Actor {
   default {
      RenderStyle "Add";
      Scale 0.076;
      +NoInteraction
   }

   override void tick() {
      setOrigin(pos + vel, true);
      a_fadeOut(0.05);
   }

   states {
   Spawn:
      SMOK A -1;
      stop;
   }
}

class Lith_Rocket : Lith_Explosive;

default {
   Damage 30;
   DamageType "Shrapnel";
   Gravity 0.2;
   Height 2;
   Radius 2;
   Scale 0.7;
   Species "Lith_Player";
   Speed 24;
   Projectile;
   +ExplodeOnWater
   +MThruSpecies
   +RocketTrail
   -NoGravity
}

vector3 m_PVel;

action void Lith_A_SpawnRocketTrail() {
   let mo = spawn("Lith_RocketTrail", pos + (0, 0, 4));
   mo.vel = (frandom(-0.5,0.5), frandom(-0.5,0.5), 0);
}

override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("effects/rocket/fly");
}

states {
Spawn:
   MISL A 0;
   #### # 0 {
      m_PVel = Vel;
      A_ChangeVelocity(Vel.X*0.05, Vel.Y*0.05, 0, CVF_REPLACE);
   }
   #### # 7 bright;
   #### # 0 {
      bNOGRAVITY = true;
      A_ChangeVelocity(m_PVel.X, m_PVel.Y, m_PVel.Z + 0.5, CVF_REPLACE);
      A_StartSound("effects/rocket/fly", lch_body, CHANF_LOOP, 0.65);
      return resolveState("SpawnLoop");
   }
   stop;
SpawnLoop:
   #### # 0 Lith_A_SpawnRocketTrail;
   #### # 1 bright;
   loop;
Death:
   TNT1 A 0 A_Explode;
   TNT1 A 0 Lith_A_ExplodeSound;
   TNT1 A 0 Lith_A_DoExplosion;
   stop;
}

/* EOF */
