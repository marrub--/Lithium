/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_OmegaRailFXBase : Actor
{
   default
   {
      RenderStyle "Add";
      Alpha 0.75;
      Translation "Lith_OmegaRail";
      Height 1;
      Radius 1;

      +NOBLOCKMAP
      +NOGRAVITY
   }
}

class Lith_OmegaRailParticle1 : Lith_OmegaRailFXBase
{
   default
   {
      Alpha 0.8;
      Translation "Lith_OmegaRailParticle";

      +NOINTERACTION
      -NOBLOCKMAP
      -NOGRAVITY
   }

   states
   {
   Spawn:
      PLSS A 10;
      PLSS A 1 A_FadeOut(0.01);
      wait;
   }
}

class Lith_OmegaRailParticle2 : Lith_OmegaRailFXBase
{
   default
   {
      Alpha 0.5;
      XScale 0.3;
      YScale 0.5;
   }

   states
   {
   Spawn:
      BFE1 C 10 bright A_FadeOut(0.1);
      loop;
   }
}

class Lith_OmegaRailBeam : Lith_OmegaRailFXBase
{
   default
   {
      RenderStyle "Add";
      Alpha 0.75;
      Translation "Lith_OmegaRail";
      XScale 0.8;
      YScale 0.7;
      Species "Lith_Player";
      DamageType "Electric";

      +DONTHARMSPECIES
      +EXTREMEDEATH
      +FORCERADIUSDMG
   }

   states
   {
   Spawn:
      BFE1 C 0 bright;
      BFE1 C 25 bright {
         A_Explode(24, 256, 0, false, 256);
         A_SpawnItemEx("Lith_OmegaRailParticle1", FRandom(-57, 57),FRandom(-57, 57),FRandom(-57, 57));
      }
      BFE1 DEF 4 bright;
      stop;
   }
}

class Lith_OmegaRailExplosion : Lith_OmegaRailFXBase
{
   default
   {
      Scale 2;

      +EXTREMEDEATH
      +FORCERADIUSDMG
   }

   states
   {
   Spawn:
      BFE1 C 0 bright;
      BFE1 C 15 bright {
         A_Explode(128, 512, 0, false, 512);
         A_PlaySound("weapons/rail/impact");
         for(int i = 0; i < 26; i++)
            A_SpawnItemEx("Lith_OmegaRailParticle2", FRandom(-32, 32),FRandom(-32, 32),FRandom(-32, 32), FRandom(-2, 2),FRandom(-2, 2),FRandom(-2, 2));
      }
      BFE1 DEF 3 bright;
      stop;
   }
}

class Lith_OmegaRailHax : FastProjectile
{
   default
   {
      Speed 32000;
      Damage 300;
      Height 1;
      Radius 1;

      +NOGRAVITY
      +NOBLOCKMAP
      +THRUACTORS
      +EXTREMEDEATH
      +EXPLODEONWATER
   }

   states
   {
   Spawn:
      TNT1 A 1;
      loop;
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_OmegaRailExplosion", flags: SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
      stop;
   }
}

/* EOF */
