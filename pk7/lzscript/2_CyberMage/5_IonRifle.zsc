// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_IonRifleFXBase : Actor
{
   default
   {
      Alpha 0.75;
      Height 1;
      Radius 1;
      RenderStyle "Add";
      Translation "Lith_IonRifleFXBase";

      +NOBLOCKMAP
      +NOGRAVITY
   }
}

class Lith_IonRifleParticle1 : Lith_IonRifleFXBase
{
   default
   {
      Alpha 0.5;
      XScale 0.5;
      YScale 0.4;
   }

   states
   {
   Spawn:
      BFE1 ABC 3 bright;
   SpawnLoop:
      BFE1 C 1 bright {
         Scale.Y *= 0.875;
         A_FadeOut(0.025);
      }
      loop;
   }
}

class Lith_IonRifleParticle2 : Lith_IonRifleFXBase
{
   default
   {
      Alpha 0.5;
      XScale 0.5;
      YScale 0.4;
   }

   states
   {
   Spawn:
      BFE2 ABC 3 bright;
   SpawnLoop:
      BFE1 C 1 bright {
         Scale.Y *= 0.875;
         A_FadeOut(0.025);
      }
      loop;
   }
}

class Lith_Ionized : PowerProtection /* HAH NOT SO PROTECTED NOW ARE YOU */
{
   default
   {
      DamageFactor "Normal", 1.5;
      Powerup.Duration -5;
   }
}

class Lith_IonRifleExplosion : Actor
{
   default
   {
      DamageType "Electric";
      Height 0;
      Radius 0;
      RenderStyle "Add";

      +NOBLOCKMAP
      +NOGRAVITY
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 A 1 A_Explode(96, 256, 0);
      TNT1 A 0 {
         if(Lith_UTIL.pData(_pdt_upgrade, UPGR_IonRifle_A))
            A_RadiusGive("Lith_Ionized", 192, RGF_MONSTERS, 1);
         for(int i = 0; i < 26; i++)
         {
            A_SpawnItemEx("Lith_IonRifleParticle1", frandom(-32, 32),frandom(-32, 32),frandom(-32, 32), frandom(-1, 1),frandom(-1, 1),frandom(-1, 1));
            A_SpawnItemEx("Lith_IonRifleParticle2", frandom(-24, 24),frandom(-24, 24),frandom(-24, 24), frandom(-0.5, 0.5),frandom(-0.5, 0.5),frandom(-0.5, 0.5));
         }
      }
      TNT1 A 22;
      stop;
   }
}

class Lith_IonRifleHax : FastProjectile
{
   default
   {
      Damage 100;
      DamageType "Electric";
      Height 1;
      Radius 1;
      Speed 32000;

      +EXPLODEONWATER
      +EXTREMEDEATH
      +NOBLOCKMAP
      +NOGRAVITY
      +THRUACTORS
   }

   states
   {
   Spawn:
      TNT1 A 1;
      loop;
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_IonRifleExplosion", flags: SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
      stop;
   }
}

class Lith_IonRifleParticle : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.05;

      +BRIGHT
      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT K 10;
   SpawnLoop:
      BLLT K 1 {
         Scale *= 1.025;
         A_FadeOut(0.075);
      }
      loop;
   }
}

class Lith_IonRifle : Lith_CWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_RocketLauncherPickup";
   Lith_Weapon.MagSize 6;
   Lith_Weapon.ScopeInSound "weapons/ionrifle/zoomin";
   Lith_Weapon.ScopeOutSound "weapons/ionrifle/zoomout";
   Tag "$LITH_INFO_SHORT_IonRifle";
   Weapon.SlotNumber 5;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/ionrifle/draw";
   Lith_Weapon.AmmoType "Lith_RocketAmmo";
   Inventory.AltHudIcon "IONGY0";

   +WEAPON.NOAUTOFIRE
}

double m_Charge;

action(Weapon) void Lith_A_FireIonRifle()
{
   Lith_A_MagIncr();
   lith_takeAmmo(2);
   lith_gunFlash();
   a_alertMonsters();
   A_FireProjectile("Lith_LightProjectileSkyLong", 0);
   A_FireProjectile("Lith_IonRifleHax", flags: FPF_NOAUTOAIM);
   if(invoker.m_Charge >= 1.5)
      A_FireProjectile("Lith_IonRifleHax", flags: FPF_NOAUTOAIM);
   if(invoker.m_Charge >= 2)
      A_FireProjectile("Lith_IonRifleHax", flags: FPF_NOAUTOAIM);
   A_RailAttack(int(invoker.m_Charge * 300), 0, false, "", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "Lith_BasicPuff", 0, 0, 0, 0, 16, 0, "Lith_IonRifleParticle");
   A_RailAttack(0, 0, false, "", "White", RGF_SILENT|RGF_FULLBRIGHT, 0, "Lith_BasicPuff", 0, 0, 0, 0, 0.5, 0);
   A_Recoil(invoker.m_Charge * 2);
   if(invoker.m_Charge >= 2)
      A_StartSound("weapons/ionrifle/fire2", lch_weapon);
   else if(invoker.m_Charge >= 1.5)
      A_StartSound("weapons/ionrifle/cfire", lch_weapon);
   else
      A_StartSound("weapons/ionrifle/fire", lch_weapon);
}

states(Weapon)
{
AltReady:
   SCOP C 1 Lith_A_Ready(WRF_NOBOB|WRF_ALLOWRELOAD);
   loop;
Ready:
   IONG A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   IONG A 0 Lith_A_ScopeOut;
DeselectLoop:
   IONG A 1 Lith_A_Lower(_raise_fast);
   loop;

Select:
   IONG A 0 Lith_A_ScopeOut;
   IONG A 1 Lith_A_Raise(_raise_fast);
   wait;

Fire:
   #### # 0 lith_jumpNoAmmo(2);
   #### # 0 Lith_A_JumpIfMagEmpty;
   #### # 0 {invoker.m_Charge = 1;}
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_IonRifle_B), 1);
   goto DoFire;
   #### # 2;
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 0 A_StartSound("weapons/ionrifle/charge", lch_weapon);
   #### # 2 offset(0, 33) {invoker.m_Charge += 0.05;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(-1, 34) {invoker.m_Charge += 0.05;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(0, 35) {invoker.m_Charge += 0.07;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(1, 36) {invoker.m_Charge += 0.07;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(0, 37) {invoker.m_Charge += 0.09;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(0, 38) {invoker.m_Charge += 0.11;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(2, 38) {invoker.m_Charge += 0.13;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(0, 39) {invoker.m_Charge += 0.15;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(-2, 40) {invoker.m_Charge += 0.16;}
   #### # 1 A_ReFire(1);
   goto DoFire;
   #### # 2 offset(2, 40) {invoker.m_Charge += 0.17;}
DoFire:
   TNT1 A 0 Lith_A_FireIonRifle;
   TNT1 A 0 A_JumpIf(countInv("Lith_WeaponScopedToken"), "FireAnim_Scoped");
FireAnim_Unscoped:
   IONG CB 2 bright;
   IONG D 1 bright lith_a_setInterpolation(true);
   IONG E 1 bright;
   IONG FGA 1;
   goto ChillCheck;
FireAnim_Scoped:
   SCOP C 1 bright Lith_A_ZoomFactorScoped(0.80);
   SCOP C 1 bright Lith_A_ZoomFactorScoped(0.85);
   SCOP C 1 Lith_A_ZoomFactorScoped(0.90);
   SCOP C 1 Lith_A_ZoomFactorScoped(0.95);
   SCOP C 3 Lith_A_ZoomFactorScoped(1.00);
ChillCheck:
   #### # 0 A_JumpIf(invoker.m_Charge < 1.3, "BoltAnim");
   IONG A 1 offset(-1, 33) {
      A_StartSound("weapons/ionrifle/heat", lch_reload);
      Lith_A_ScopeOut();
   }
   IONG A 2 offset(-4, 34) A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
   IONG A 2 offset(-9, 37) A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
   IONG A 3 offset(-11, 39) A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
   IONG A 4 offset(-12, 40) A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
   IONG A 5 offset(-13, 41) A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
ChillOut2:
   IONG A 0 A_FireProjectile("Lith_GunSmokeEmitter", 0, 0, 2, 5);
   IONG A 2 A_JumpIf((invoker.m_Charge -= 0.06) < 1, "ChilledOut");
   loop;
ChilledOut:
   IONG A 4 offset(-12, 40);
   IONG A 3 offset(-11, 39);
   IONG A 2 offset(-9, 37);
   IONG A 2 offset(-4, 34);
   IONG A 1 offset(-1, 33);
   goto BoltAnim;
BoltAnim:
   #### # 1 offset(1, 32);
   #### # 1 offset(3, 32);
   #### # 1 offset(4, 32);
   #### # 1 offset(8, 38);
   #### # 1 offset(12, 45);
   #### # 1 offset(16, 53);
   #### # 1 offset(19, 60) A_StartSound("weapons/ionrifle/eject", lch_reload2);
   #### # 1 offset(21, 64);
   #### # 1 offset(23, 67);
   #### # 1 offset(22, 74);
   #### # 1 offset(19, 78);
   #### # 5 offset(18, 80);
   #### # 5 offset(17, 80);
   #### # 5 offset(16, 80) A_StartSound("weapons/ionrifle/load", lch_reload3);
   #### # 1 offset(17, 74);
   #### # 1 offset(19, 67);
   #### # 1 offset(20, 66);
   #### # 1 offset(21, 62);
   #### # 1 offset(20, 60);
   #### # 1 offset(21, 63);
   #### # 1 offset(23, 66);
   #### # 1 offset(24, 68);
   #### # 1 offset(25, 63);
   #### # 1 offset(24, 59);
   #### # 1 offset(22, 57);
   #### # 1 offset(20, 54);
   #### # 1 offset(16, 49);
   #### # 1 offset(12, 44);
   #### # 1 offset(10, 42);
   #### # 1 offset(8, 40);
   #### # 1 offset(5, 38);
   #### # 1 offset(2, 35);
   #### # 5 offset(1, 32) lith_a_setInterpolation(false);
   #### # 0 Lith_A_JumpIfMagEmpty(false);
   goto ReadySelector;

AltFire:
   #### # 1;
   #### # 0 Lith_A_ScopeToggle;
   goto ReadySelector;

Reload:
   IONG A 0 A_JumpIf(invoker.m_Fired != invoker.m_Max, "ReadySelector");
   IONG A 0 Lith_A_ScopeOut;
   IONG A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 0 Lith_A_ReloadBobDn(0.2);
   IONG A 1 offset(-2, 34) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 0 Lith_A_ReloadBobDn(0.2);
   IONG A 1 offset(-4, 37) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 0 Lith_A_ReloadBobDn(0.2);
   IONG A 1 offset(-9, 43) A_StartSound("weapons/ionrifle/magout", lch_reload);
   IONG A 0 Lith_A_ReloadBobDn(0.2);
   IONG A 1 offset(-12, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(-14, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 2 offset(-12, 55) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 3 offset(-11, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 8 offset(-10, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 8 offset(-11, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 8 offset(-12, 60) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 0 Lith_A_ReloadFinish;
   IONG A 2 offset(-13, 59) A_StartSound("weapons/ionrifle/magin", lch_reload2);
   IONG A 2 offset(-11, 49) Lith_A_ReloadBobUp(0.4);
   IONG A 2 offset(-11, 47) Lith_A_ReloadBobUp(0.4);
   IONG A 7 offset(-11, 45);
   IONG A 7 offset(-12, 45);
   IONG A 1 offset(-12, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(-8, 53) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(-1, 57) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(3, 56) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(8, 52) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(10, 49) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(8, 47) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 1 offset(7, 45) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 2 offset(5, 43) A_WeaponReady(WRF_NOBOB|WRF_NOFIRE);
   IONG A 2 offset(3, 40) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   IONG A 3 offset(2, 39) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   IONG A 3 offset(1, 37) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   IONG A 4 offset(1, 34) A_WeaponReady(WRF_NOBOB|WRF_NOSECONDARY);
   IONG A 4 A_WeaponReady(WRF_NOBOB);
   goto ReadySelector;
FireEmpty:
   #### # 1;
   #### # 2 offset(0, 41);
   #### # 1 offset(0, 45);
   #### # 1 offset(0, 43);
   #### # 1 offset(0, 40) A_StartSound("weapons/ionrifle/empty", lch_dryweapon);
   #### # 1 offset(0, 38);
   #### # 1 offset(0, 36);
   #### # 1 offset(0, 33);
   #### # 1 offset(0, 32);
   #### # 5;
   #### #### 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
