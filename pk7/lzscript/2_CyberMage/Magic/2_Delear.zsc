// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_DelearDotTrail : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.4;

      +ForceXyBillboard
      +NoInteraction
      +NoTimeFreeze
   }

   states
   {
   Spawn:
      BLLT J 0 bright A_SetScale(scale.x * 0.8);
      BLLT J 1 bright A_FadeOut(0.1);
      loop;
   }
}

class Lith_DelearSprite : Lith_DelearDotTrail
{
   default
   {
      Scale 0.3;

      +ForceXyBillboard
   }

   states
   {
   Spawn:
      BLLT J 0;
      BLLT J 1 bright;
      stop;
   }
}

class Lith_DelearExplosion : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.35;

      +ForceXyBillboard
      +NoInteraction
   }

   states
   {
   Spawn:
      MICX ABCDEFG 1 bright;
      stop;
   }
}

class Lith_DelearDot : FastProjectile
{
   default
   {
      DamageFunction 20 * random(1, 2);
      DamageType "Magic";
      Height 2;
      MissileHeight 8;
      MissileType "Lith_DelearDotTrail";
      Radius 4;
      Speed 60;
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   Death:
      TNT1 A 0 A_SpawnItemEx("Lith_DelearExplosion");
      stop;
   }
}

class Lith_Delear : Lith_CWeapon;

default
{
   Lith_Weapon.MagSize 16;
   Tag "$LITH_INFO_SHORT_Delear";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/delear/draw";
   Lith_Weapon.AmmoType "Lith_ManaAmmo";
   Inventory.AltHudIcon "SPELG0";

   +Weapon.DontBob
   -Weapon.NoAutoFire
   -Lith_Weapon.HitScan
}

states(Weapon)
{
Ready:
   TNT1 A 1 Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 Lith_A_Lower(_raise_instant);
   loop;
Select:
   TNT1 A 0 ACS_ScriptCall("Lith_MagicSelect", 2);
SelectLoop:
   TNT1 A 0 Lith_A_Raise(_raise_instant);
   loop;

Fire:
   TNT1 A 0 Lith_A_JumpIfMagEmpty(false);
   TNT1 A 4 {
      int xx, yy;
      switch(invoker.m_Fired) {
      case  0: xx = -8;  yy = 18; break;
      case  1: xx = -12; yy = 16; break;
      case  2: xx = -16; yy = 14; break;
      case  3: xx = -20; yy = 12; break;
      case  4: xx = -8;  yy = 8 ; break;
      case  5: xx = -12; yy = 6 ; break;
      case  6: xx = -16; yy = 4 ; break;
      case  7: xx = -20; yy = 2 ; break;
      case  8: xx =  8;  yy = 18; break;
      case  9: xx =  12; yy = 16; break;
      case 10: xx =  16; yy = 14; break;
      case 11: xx =  20; yy = 12; break;
      case 12: xx =  8;  yy = 8 ; break;
      case 13: xx =  12; yy = 6 ; break;
      case 14: xx =  16; yy = 4 ; break;
      case 15: xx =  20; yy = 2 ; break;
      }
      A_FireProjectile("Lith_DelearDot", 0, false, xx, yy);
      Lith_A_MagIncr();
      lith_gunFlash();
      soundAlert(invoker.owner);
      A_StartSound("weapons/delear/fire", lch_weapon);
   }
   goto ReadySelector;

Reload:
   TNT1 A 0 lith_jumpNoAmmo(25);
   TNT1 A 18 {
      lith_takeAmmo(25);
      A_StartSound("weapons/delear/open", lch_reload);
   }
   TNT1 A 9 A_StartSound("weapons/delear/charge", lch_reload2);
   TNT1 A 4 Lith_A_ReloadFinish;
   TNT1 A 1 A_StartSound("weapons/delear/close", lch_reload);
   goto ReadySelector;
}

/* EOF */
