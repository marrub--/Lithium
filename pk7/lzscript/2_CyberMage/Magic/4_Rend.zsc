// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RendHit : FastProjectile
{
   default
   {
      DamageFunction 50 * random(1, 2);
      Height 8;
      Radius 8;
      Speed 196;

      +FORCEPAIN
   }

   states
   {
   Spawn:
      TNT1 A 1;
      stop;
   XDeath:
      TNT1 A 0 {
         if(target) {
            target.A_StartSound("weapons/rend/hit", lch_body);
            target.ACS_ScriptCall("Lith_Rend", true);
         }
      }
      stop;
   }
}

class Lith_RendHit2 : Lith_RendHit {default {DamageFunction 50 * random(2, 3);}}

class Lith_Rend : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Rend";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/rend/draw";
   Lith_Weapon.AmmoType "Lith_ManaAmmo";
   Inventory.AltHudIcon "SPELB0";

   +Weapon.NoAutoFire
   +Inventory.Unclearable
   -Lith_Weapon.HitScan
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 Lith_A_Lower(_raise_instant);
   loop;

Select:
   TNT1 A 0 ACS_ScriptCall("Lith_MagicSelect", 4);
SelectLoop:
   TNT1 A 0 Lith_A_Raise(_raise_instant);
   loop;

Fire:
   TNT1 A 0 lith_jumpNoAmmo(10, "ReadySelector");
   TNT1 A 5 {
      lith_takeAmmo(10);
      CallACS("Lith_Rend", false, random(0, 3));
      A_StartSound("weapons/rend/slash", lch_weapon);
      A_FireProjectile("Lith_BladeLightProjectile", 0);
      SoundAlert(Invoker.Owner);
      if(CountInv("PowerStrength"))
         A_FireProjectile("Lith_RendHit2");
      else
         A_FireProjectile("Lith_RendHit");
   }
   TNT1 A 0 A_ReFire;
   TNT1 A 5 A_StartSound("weapons/rend/end", lch_weapon2);
   goto ReadySelector;
}

/* EOF */
