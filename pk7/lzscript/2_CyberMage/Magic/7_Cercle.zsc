// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_CircleSpearTrail : Actor
{
   default
   {
      RenderStyle "Add";

      +BRIGHT
      +NOINTERACTION
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      BLLT S 1 A_FadeOut(0.2);
      wait;
   }
}

class Lith_CircleSpear : Actor
{
   default
   {
      DamageFunction 77;
      DamageType "Magic";
      RenderStyle "Add";
      Species "Lith_Player";

      Projectile;
      +BRIGHT
      +DONTHARMSPECIES
      +MTHRUSPECIES
      +NOTIMEFREEZE
      +RIPPER
   }

   int m_Trigger;

   states
   {
   Spawn:
      BLLT S 1 A_JumpIf(m_Trigger, "Go");
      wait;
   Go:
      BLLT S random(0, 2);
      BLLT S 3 {
         int v = Random(20, 30);
         A_SpawnItemEx("Lith_CircleSpearTrail");
         A_ChangeVelocity(Cos(Angle)*v, Sin(Angle)*v, 0, CVF_REPLACE);
      }
      wait;
   Death:
      BLLT S 1 A_FadeOut;
      wait;
   }
}

class Lith_CircleSpearThrower : Actor
{
   default
   {
      RenderStyle "None";

      +NOCLIP
      +NOGRAVITY
      +NOTIMEFREEZE
   }

   states
   {
   Spawn:
      TNT1 A 0;
      TNT1 AAAAAAAAAAAAAA 0 A_SpawnItemEx("Lith_CircleSpear", frandom(-8,8),frandom(-8,8),frandom(-16,16), 0,0,0, frandom(-60,60), SXF_TRANSFERPOINTERS, 0, tid);
      stop;
   }
}

class Lith_CircleElectricity : Actor
{
   default
   {
      Damage 7;
      DamageType "Magic";
      RenderStyle "Add";
      Species "Lith_Player";
      Translation "Lith_CircleElectricity";

      Projectile;
      +BRIGHT
      +DONTHARMSPECIES
      +MTHRUSPECIES
      +NOTIMEFREEZE
      +RANDOMIZE
      +RIPPER
   }

   states
   {
   Spawn:
      ZAP1 FFEFEFDDEFDEEDEEDDDCCCBCBBBCBBBBAAABA 1 A_FadeOut(0.01);
      ZAP1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.02);
      stop;
   }
}

class Lith_CircleWallFx : Actor
{
   default
   {
      Alpha 0.7;
      DamageType "Magic";
      RenderStyle "Add";
      Species "Lith_Player";
      XScale 0.7;
      YScale 5.1;

      +DONTHARMSPECIES
      +NOTIMEFREEZE
      +NOCLIP
      +NOGRAVITY
      +BRIGHT
   }

   states
   {
   Spawn:
      BLLT E 0;
      BLLT EEE 1 A_FadeIn;
      BLLT E 0 A_Explode(128, 128, xf_nosplash);
   FadeLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha <= 0.05, "Fire");
      loop;
   Fire:
      TNT1 A 2;
      TNT1 AA 0 A_SpawnItemEx("Lith_CircleElectricity", frandom(-32,32),frandom(-32,32),0, frandom(-4,4),frandom(-4,4),frandom(-4,4), 0, SXF_TRANSFERPOINTERS);
      stop;
   }
}

class Lith_CircleWallTrail : Lith_CircleWallFx
{
   default
   {
      XScale 0.5;
      YScale 5;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      BLLT E 1 A_FadeOut;
      wait;
   Nope:
      TNT1 A 0;
      stop;
   }
}

class Lith_CircleWall : Lith_CircleWallFx
{
   default
   {
      DamageType "Magic";
      Height 32;
      Radius 8;
      Species "Lith_Player";
      XScale 0.5;
      YScale 5;

      +DONTHARMSPECIES
   }

   int m_Trigger;

   states
   {
   Spawn:
      BLLT E 1 A_ScaleVelocity(0.9);
      BLLT E 0 A_JumpIf(m_Trigger, "Fire");
      loop;
   Fire:
      BLLT E 0 {
         A_SpawnItemEx("Lith_CircleWallFX", 0,0,0, 0,0,0, 0, SXF_TRANSFERPOINTERS);
         m_Trigger = false;
      }
   FireDoneLoop:
      BLLT E 1 A_ScaleVelocity(0.9);
      BLLT E 0 A_JumpIf(m_Trigger, "FireOut");
      loop;
   FireOut:
      BLLT E 1 A_ChangeVelocity(Cos(Angle)*4, Sin(Angle)*4, 0, CVF_REPLACE);
   FireOutLoop:
      BLLT E 1 {
         A_ChangeVelocity(vel.x*1.34, vel.y*1.34, 0, CVF_REPLACE);
         A_FadeOut(0.05);
         A_Explode(128, 128, xf_nosplash);
         A_SpawnItemEx("Lith_CircleWallTrail", -8);
      }
      loop;
   }
}

class Lith_CircleParticle : Actor
{
   default
   {
      RenderStyle "Add";
      Scale 0.7;

      +BRIGHT
      +NOINTERACTION
      +NOTIMEFREEZE
   }

   int m_Trigger;
   int m_FxTID;

   states
   {
   Spawn:
      BLLT E 1;
   SpawnLoop:
      BLLT E 1 A_FadeOut(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha <= 0.05, "FadeOutDone");
      loop;
   FadeOutDone:
      BLLT E 1 A_JumpIf(m_Trigger, "DoFadeIn");
      loop;
   DoFadeIn:
      BLLT E 1 A_FadeIn(0.1, FTF_CLAMP);
      BLLT E 0 A_JumpIf(alpha >= 1, "Done");
      loop;
   Done:
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,7, 0, SXF_TRANSFERPOINTERS, 0, m_FxTID);
      BLLT E 1 A_SpawnItemEx("Lith_CircleWall", 0,0,0, 0,0,2, 0, SXF_TRANSFERPOINTERS, 0, m_FxTID);
      BLLT E 1 A_FadeOut(0.34);
      stop;
   }
}

class Lith_CircleSprite : Actor {
   default {
      RenderStyle "Add";
      FloatBobStrength 2.0;

      +BRIGHT
      +NOINTERACTION
      +NOTIMEFREEZE
      +FLOATBOB
   }

   double m_ofs;

   states {
   Spawn:
      BLLT W 1;
      BLLT W 1 {
         A_Warp(AAPTR_PLAYER1, 64,0,32, (level.time+m_ofs)*1.7%180.0-90.0, WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
         return Lith_UTIL.pData(_pdt_weapon) != weapon_c_cercle ? ResolveState("Null") : state(null);
      }
      wait;
   }
}

class Lith_Cercle : Lith_CWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Cercle";
   Weapon.SlotNumber 0;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/cercle/draw";
   Lith_Weapon.AmmoType "Lith_ManaAmmo";
   Inventory.AltHudIcon "SPELE0";

   +Weapon.NoAutoFire
   +Inventory.Unclearable
}

states(Weapon)
{
Ready:
   TNT1 A 1 bright Lith_A_Ready;
   loop;

Deselect:
   TNT1 A 0 Lith_A_Lower(_raise_instant);
   loop;

Select:
   TNT1 A 0 ACS_ScriptCall("Lith_MagicSelect", 7);
   TNT1 A 0 {
      for(int i = 0; i < 5; ++i) {
         Lith_CircleSprite(Spawn("Lith_CircleSprite", pos)).m_ofs = 45.0 * i;
      }
   }
SelectLoop:
   TNT1 A 0 Lith_A_Raise(_raise_instant);
   loop;

Fire:
   TNT1 A 0 A_JumpIf(countInv("Lith_ManaAmmo") == lith_maxInv("Lith_ManaAmmo"), 1);
   goto ReadySelector;
   TNT1 A 10 {
      lith_takeAmmo(int.max);
      A_AlertMonsters();
      ACS_NamedExecuteWithResult("Lith_Cercle", false);
   }
   goto ReadySelector;
}

/* EOF */
