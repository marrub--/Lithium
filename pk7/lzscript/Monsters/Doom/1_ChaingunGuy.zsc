/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_XH_ChaingunGuy : Lith_ChaingunGuy
{
   override void Lith_A_ChaingunGuyShot()
   {
      A_StartSound("enemies/chainguy/fire", lch_weapon);
      A_FaceTarget();
      if(!Lith_BarrierBullets())
         A_WolfAttack(WAF_NORANDOM | WAF_USEPUFF, "", 2, 20, 256, 4, 6);
   }
}

class Lith_ChaingunGuy : Lith_Monster replaces ChaingunGuy;

default
{
   Health     60;
   Height     56;
   Mass       100;
   PainChance 170;
   Radius     20;
   Speed      9;

   ActiveSound "chainguy/active";
   DeathSound  "chainguy/death";
   PainSound   "chainguy/pain";
   SeeSound    "chainguy/sight";

   Obituary "$OB_CHAINGUY";
   Tag      "Former Corporal";

   +FLOORCLIP
}

virtual void Lith_A_ChaingunGuyShot()
{
   A_StartSound("enemies/chainguy/fire", lch_weapon);
   A_FaceTarget();
   if(!Lith_BarrierBullets())
      A_CustomBulletAttack(22.5, 0, 1, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM);
}

states
{
Spawn:
   CPOS AB 10 A_Look;
   loop;

See:
   CPOS AABBCCDD 3 A_Chase;
   loop;

Missile:
   CPOS E 2 A_FaceTarget;
   CPOS E 0 A_StartSound("enemies/chainguy/bolt", lch_weapon, CHANF_NOPAUSE);
   CPOS EE 6 A_FaceTarget;
MissileLoop:
   CPOS F 4 bright Lith_A_ChaingunGuyShot;
   CPOS E 4 bright Lith_A_ChaingunGuyShot;
   CPOS F 1 A_CPosRefire;
   loop;

Pain:
   CPOS G 3;
   CPOS G 3 A_Pain;
   CPOS G 0 ResolveState("See");
   stop;

Death:
   CPOS H -1;
Crash:
   CPOS H 5 Lith_A_Die;
   CPOS IJKLM 5;
   CPOS N -1;
   stop;
Death.Extreme:
   CPOS O -1;
Crash.Extreme:
   CPOS O 5 Lith_A_XDie;
   CPOS PQRS 5;
   CPOS T -1;
   stop;

Raise:
   CPOS NMLKJIH 5;
   CPOS H 0 ResolveState("See");
   stop;
}

/* EOF */
