class Lith_XH_ChaingunGuy : Lith_ChaingunGuy
{
   override void Lith_A_ChaingunGuyShot()
   {
      A_PlaySound("enemies/chainguy/fire");
      A_FaceTarget();
      if(!Lith_BarrierBullets())
         A_WolfAttack(WAF_NORANDOM | WAF_USEPUFF, "", 2, 20, 256, 4, 6);
   }
}

class Lith_ChaingunGuy : Lith_Monster replaces ChaingunGuy;

default
{
   Lith_Monster.Score Score_ChaingunGuy;
   Health 60;
   Radius 20;
   Height 56;
   Mass 100;
   Speed 9;
   PainChance 170;
   SeeSound "chainguy/sight";
   PainSound "chainguy/pain";
   DeathSound "chainguy/death";
   ActiveSound "chainguy/active";
   Obituary "$OB_CHAINGUY";
   Tag "Former Corporal";

   +FLOORCLIP;
}

virtual void Lith_A_ChaingunGuyShot()
{
   A_PlaySound("enemies/chainguy/fire");
   A_FaceTarget();
   if(!Lith_BarrierBullets())
      A_CustomBulletAttack(22.5, 0, 1, random(1, 5) * 3, "BulletPuff", 0, CBAF_NORANDOM);
}

states
{
Spawn:
   CPOS AB 10 A_Look;
   loop;

See:
   CPOS AABBCCDD 3 A_Chase;
   loop;

Missile:
   CPOS E 2 A_FaceTarget;
   CPOS E 0 A_PlaySound("enemies/chainguy/bolt", CHAN_WEAPON|CHAN_NOPAUSE);
   CPOS E 0 A_GiveToTarget("Lith_AdrenalineToken", 1);
   CPOS EE 6 A_FaceTarget;
MissileLoop:
   CPOS F 4 bright Lith_A_ChaingunGuyShot;
   CPOS E 4 bright Lith_A_ChaingunGuyShot;
   CPOS F 1 A_CPosRefire;
   loop;

Pain:
   CPOS G 3;
   CPOS G 3 A_Pain;
   CPOS G 0 ResolveState("See");
   stop;

Death:
   CPOS H -1;
Crash:
   CPOS H 5 Lith_A_Die;
   CPOS IJKLM 5;
   CPOS N -1;
   stop;
Death.Extreme:
   CPOS O -1;
Crash.Extreme:
   CPOS O 5 Lith_A_XDie;
   CPOS PQRS 5;
   CPOS T -1;
   stop;

Raise:
   CPOS NMLKJIH 5;
   CPOS H 0 ResolveState("See");
   stop;
}

// EOF
