// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_BaronHellfireTrailSpawner : Actor {
   default {
      Height 2;
      Radius 2;
      +NoInteraction
   }

   override void postBeginPlay() {
      a_spawnItemEx("Lith_BaronHellfireTrail", -16, 0, 0, frandom(0.0, -1.0));
      a_spawnParticle("37732B", SPF_FULLBRIGHT | SPF_RELATIVE, 25, 5, 0, frandom(-8,8), frandom(-8,8), frandom(-2,2), frandom(0, 10), frandom(-0.1, 0.1), frandom(-1, 1), 0, 0, 0.02, 1.0, -1);
      destroy();
   }
}

class Lith_BaronHellfireTrail : Actor
{
   default
   {
      Alpha       0.7;
      Height      2;
      Radius      2;
      Renderstyle "Translucent";
      Scale       0.666;

      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      BAL7 C 2;
      BAL7 DE 3 A_FadeOut(0.333);
      stop;
   }
}

class Lith_BaronBall : BaronBall replaces BaronBall
{
   default
   {
      Alpha       1;
      Damage      4;
      DamageType  "Magic";
      RenderStyle "Translucent";

      +RANDOMIZE
   }

   states
   {
   Spawn:
      BAL7 A 0;
      BAL7 AAAABBBB 1 bright A_SpawnItemEx("Lith_BaronHellfireTrailSpawner", 0, frandom(-0.5, 0.5), 0, frandom(0,-1));
      loop;
   Death:
      BAL7 CDE 6 bright;
      stop;
   }
}

class Lith_BaronBallSilent : Lith_BaronBall
{
   default
   {
      DeathSound "";
      SeeSound   "";
   }
}

class Lith_XH_BaronOfHell : Lith_BaronOfHell
{
}

class Lith_BaronOfHell : Lith_Monster replaces BaronOfHell;

default
{
   BloodColor "DarkGreen";
   Health     1000;
   Height     64;
   Mass       1000;
   PainChance 55;
   Radius     24;
   Species    "Lith_HellKnight";
   Speed      8;

   ActiveSound "baron/active";
   DeathSound  "baron/death";
   PainSound   "baron/pain";
   SeeSound    "baron/sight";

   HitObituary "$OB_BARONHIT";
   Obituary    "$OB_BARON";
   Tag         "$LITH_INFO_SHORT_BaronOfHell";

   +E1M8Boss
   +BossDeath
   +FloorClip
   +MissileMore
}
override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("baron/melee");
}

int m_Angle;

states
{
Spawn:
   BOSS AB 10 A_Look;
   loop;

See:
   BOSS AABBCCDD 3 A_Chase;
   loop;

Melee:
   BOSS EF 6 A_FaceTarget;
   BOSS G 4 A_CustomMeleeAttack(10 * random(1, 8), "baron/melee");
   goto See;

Missile:
   BOSS EF 4 A_FaceTarget;
   BOSS G 0 A_FaceTarget;
   BOSS G 1 A_SpawnProjectile("Lith_BaronBall", 32, 0, 0, CMF_BADPITCH);
   BOSS G 0 A_Jump(32, "MissileDone");
   BOSS G 0 A_FaceTarget;
   BOSS G 0 {m_Angle = randompick(-45, 45, 0);}
   BOSS G 1 A_SpawnProjectile("Lith_BaronBallSilent", 32, 0, m_Angle, CMF_AIMDIRECTION|CMF_BADPITCH);
   BOSS G 0 ResolveState("See");
MissileDone:
   BOSS G 3;
   BOSS G 0 ResolveState("See");
   stop;

Pain:
   BOSS H 1;
   BOSS H 1 A_Pain;
   goto See;

Death:
   BOSS I -1;
Crash:
   BOSS I 8 Lith_A_Die;
   BOSS JKLMN 8;
   BOSS O -1 A_BossDeath;
   stop;
Death.Extreme:
   #MBA A -1;
Crash.Extreme:
   #MBA A 5 Lith_A_XDie;
   #MBA BCDEFGH 5;
   #MBA I -1 A_BossDeath;
   stop;

Raise:
   BOSS ONMLKJI 8;
   BOSS A 0 ResolveState("See");
   stop;
}

/* EOF */
