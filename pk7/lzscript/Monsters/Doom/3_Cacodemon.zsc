// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_CacodemonBall : CacodemonBall replaces CacodemonBall
{
   default
   {
      DamageType "Electric";
   }

   states
   {
   Spawn:
      BAL2 AB 4 bright;
      loop;
   }
}

class Lith_XH_Cacodemon : Lith_Cacodemon
{
}

class Lith_Cacodemon : Lith_Monster replaces Cacodemon;

default
{
   BloodColor "Blue";
   Health     400;
   Height     56;
   Mass       400;
   PainChance 128;
   Radius     31;
   Speed      8;

   ActiveSound "caco/active";
   DeathSound  "caco/death";
   PainSound   "caco/pain";
   SeeSound    "caco/sight";

   HitObituary "$OB_CACOHIT";
   Obituary    "$OB_CACO";
   Tag         "$LITH_INFO_SHORT_Cacodemon";

   +FLOAT
   +NOGRAVITY
}

states
{
Spawn:
   HEAD A 10 A_Look;
   loop;

See:
   HEAD A 3 A_Chase;
   loop;

Missile:
   HEAD BC 5 A_FaceTarget;
   HEAD D 5 bright A_HeadAttack;
   HEAD D 0 ResolveState("See");
   stop;

Pain:
   HEAD E 3;
   HEAD E 3 A_Pain;
   HEAD F 6;
   HEAD F 0 ResolveState("See");
   stop;

Death:
   HEAD G -1;
Crash:
   HEAD G 8 {Lith_A_Die(); A_SetFloorClip();}
   HEAD HIJK 8;
   HEAD L -1;
   stop;
Death.Extreme:
   CACX A -1;
Crash.Extreme:
   CACX A 5 Lith_A_XDie;
   CACX BC 5;
   CACX D 4;
   CACX E 3;
   CACX F 4;
   CACX G 5;
   CACX H -1;
   stop;

Raise:
   HEAD L 8 A_UnsetFloorClip;
   HEAD KJIHG 8;
   HEAD A 0 ResolveState("See");
   stop;
}

/* EOF */
