// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_XH_ZombieMan : Lith_ZombieMan
{
   default
   {
      MinMissileChance 120;
   }

   override void Lith_A_ZombieManShot()
   {
      if(!Lith_BarrierBullets())
         if(CheckIfCloser(target, 1024, false))
            A_WolfAttack(WAF_NORANDOM|WAF_USEPUFF, "enemies/zombie/fire", 3.0 / 4.0, 20);
         else
            A_WolfAttack(WAF_NORANDOM, "enemies/chainguy/fire", 1.0 / 5.0, 60);
   }
}

class Lith_ZombieMan : Lith_Monster replaces ZombieMan;

default
{
   Health           30;
   Height           56;
   MinMissileChance 150;
   PainChance       200;
   Radius           20;
   Speed            9;
   Species          "Lith_ZombieMan";

   DropItem "Lith_Clip", 255, 5;

   ActiveSound "grunt/active";
   DeathSound  "grunt/death";
   PainSound   "grunt/pain";
   SeeSound    "grunt/sight";

   Obituary "$OB_ZOMBIE";
   Tag      "$LITH_INFO_SHORT_ZombieMan";

   +FLOORCLIP
}

override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("enemies/chainguy/fire");
   markSound("enemies/zombie/fire");
   markSound("enemies/zombie/ready");
}
virtual void Lith_A_ZombieManShot()
{
   A_StartSound("enemies/zombie/fire", lch_weapon);
   if(!Lith_BarrierBullets())
      A_CustomBulletAttack(22.5, 5, 1, random(1, 4) * 3, "BulletPuff", 0, CBAF_NORANDOM);
}

int m_Shots;

states
{
Spawn:
   POSS AB 10 A_Look;
   loop;

See:
   POSS AABBCCDD 4 A_Chase;
   loop;

Missile:
   POSS E 0 {m_Shots = random(1, 3);}
   POSS E 2 A_FaceTarget;
   POSS E 0 A_StartSound("enemies/zombie/ready", lch_body, CHANF_NOPAUSE);
   POSS E 5 A_FaceTarget;
MissileLoop:
   POSS E 5 A_FaceTarget;
   POSS F 4 bright Lith_A_ZombieManShot;
   POSS F 0 A_JumpIf(--m_Shots == 0, "MissileEnd");
   loop;
MissileEnd:
   POSS E 8;
   POSS E 0 ResolveState("See");
   stop;

Pain:
   POSS G 3;
   POSS G 3 A_Pain;
   POSS G 0 ResolveState("See");
   stop;

Death:
   POSS H -1;
Crash:
   POSS H 5 Lith_A_Die;
   POSS IJK 5;
   POSS L -1;
   stop;
Death.Extreme:
   POSS M -1;
Crash.Extreme:
   POSS M 5 Lith_A_XDie;
   POSS NOPQRST 5;
   POSS U -1;
   stop;

Raise:
   POSS K 5;
   POSS JTH 5;
   POSS A 0 ResolveState("See");
   stop;
}

/* EOF */
