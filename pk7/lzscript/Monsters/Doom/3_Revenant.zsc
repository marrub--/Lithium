// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_RevenantTracer : RevenantTracer replaces RevenantTracer
{
   default
   {
      DamageType "Shrapnel";
   }

   int m_Timer;
   override void markPrecacheSounds() {
      super.markPrecacheSounds();
      markSound("enemies/revenant/home");
   }

   states
   {
   Spawn:
      FATB A 0;
      FATB A 0 A_JumpIf(m_Timer++ > 35 * 10, "Death");
      FATB AB 2 bright {
         if(level.time & 3 == 0) {
            if(level.time % 20 == 0) A_StartSound("enemies/revenant/home", lch_body);
            A_SpawnItemEx("Lith_RifleGrenadeSmoke", -8);
            A_SeekerMissile(1, 45, 0);
         }
      }
      loop;
   }
}

class Lith_XH_RevenantTracer : Lith_RevenantTracer
{
   default
   {
      Damage 1;
      Height 2;
      Radius 2;
      Scale  0.7;
   }

   states
   {
   Spawn:
      FATB A 0;
      FATB A 0 A_JumpIf(m_Timer++ > 35 * 5, "Death");
      FATB AB 2 bright A_SeekerMissile(1, 45, 0);
      loop;
   }
}

class Lith_XH_Revenant : Lith_Revenant
{
   default
   {
      Health 200;

      +ALWAYSFAST
   }

   states
   {
   Spawn:
      SKEL A 0;
      SKEL A 0 {if(random(0, 100) < 10) {A_SetTranslucent(0.05, 0);}}
      goto Super::Spawn;
   Missile:
      SKEL J 5 bright A_FaceTarget;
   Missile1:
      SKEL K 10 A_SpawnProjectile("Lith_XH_RevenantTracer", 48, 0, 0, CMF_BADPITCH);
      SKEL K random(2, 4) A_FaceTarget;
      SKEL K 0 A_MonsterRefire(0, "See");
      goto Missile1;
   Melee:
      SKEL G 0 A_FaceTarget;
      SKEL G 2 A_SkelWhoosh;
      SKEL H 2 A_FaceTarget;
      SKEL I 2 A_SkelFist;
      goto See;
   }
}

class Lith_Revenant : Lith_Monster replaces Revenant;

default
{
   Health         350;
   Height         56;
   Mass           500;
   MeleeThreshold 196;
   PainChance     100;
   Radius         20;
   Speed          10;
   Species        "Lith_Revenant";

   ActiveSound "skeleton/active";
   DeathSound  "skeleton/death";
   MeleeSound  "skeleton/melee";
   PainSound   "skeleton/pain";
   SeeSound    "skeleton/sight";

   HitObituary "$OB_UNDEADHIT";
   Obituary    "$OB_UNDEAD";
   Tag         "$LITH_INFO_SHORT_Revenant";

   +FLOORCLIP
   +MISSILEMORE
}

states
{
Spawn:
   SKEL AB 10 A_Look;
   loop;

See:
   SKEL AABBCCDDEEFF 2 A_Chase;
   loop;

Melee:
   SKEL G 0 A_FaceTarget;
   SKEL G 6 A_SkelWhoosh;
   SKEL H 6 A_FaceTarget;
   SKEL I 6 A_SkelFist;
   SKEL I 0 ResolveState("See");
   stop;

Missile:
   SKEL J 10 bright A_FaceTarget;
   SKEL K 10 A_SkelMissile;
   SKEL K 10 A_FaceTarget;
   SKEL K 0 ResolveState("See");
   stop;

Pain:
   SKEL L 5;
   SKEL L 5 A_Pain;
   SKEL L 0 ResolveState("See");
   stop;

Death:
   SKEL L -1;
Crash:
   SKEL L 7 Lith_A_Die;
   SKEL MNOP 7;
   SKEL Q -1;
   stop;
Death.Extreme:
   LI08 Q -1;
Crash.Extreme:
   LI08 Q 3 Lith_A_XDie;
   LI08 R 4;
   LI08 STU 5;
   LI08 V -1;
   stop;

Raise:
   SKEL QPONML 5;
   SKEL A 0 ResolveState("See");
   stop;
}

/* EOF */
