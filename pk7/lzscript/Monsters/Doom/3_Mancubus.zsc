// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_FatShot : FatShot replaces FatShot
{
   default
   {
      Damage     7;
      DamageType "Fire";
      Speed      10;
   }

   states
   {
   Spawn:
      MANF AB 4 bright;
      loop;
   }
}

class Lith_XH_FatShot : Lith_FatShot
{
   default
   {
      Species "Lith_XH_Mancubus";
      Speed   16;

      +MTHRUSPECIES
   }
}

class Lith_XH_Mancubus : Lith_Mancubus
{
   states
   {
   Missile:
      FATT G 14 A_FatRaise;
      FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
      FATT HH 5 bright A_FatAttack1("Lith_XH_FatShot");
      FATT IG random(4,5) A_FaceTarget;
      FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
      FATT HH 5 bright A_FatAttack2("Lith_XH_FatShot");
      FATT IG random(4,5) A_FaceTarget;
      FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
      FATT HH 5 bright A_FatAttack3("Lith_XH_FatShot");
      FATT IG random(4,5) A_FaceTarget;
      FATT G 0 ResolveState("See");
      stop;
   }
}

class Lith_Mancubus : Lith_Monster replaces Fatso;

default
{
   Health     600;
   Height     64;
   Mass       1000;
   PainChance 50;
   Radius     48;
   Speed      8;
   Species    "Lith_Mancubus";

   ActiveSound "fatso/active";
   DeathSound  "fatso/death";
   PainSound   "fatso/pain";
   SeeSound    "fatso/sight";

   Obituary "$OB_FATSO";
   Tag      "$LITH_INFO_SHORT_Mancubus";

   +BossDeath
   +Map07Boss1
   +FloorClip
}
override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("enemies/mancubus/fire");
}

states
{
Spawn:
   FATT AB 15 A_Look;
   loop;

See:
   FATT AABBCCDDEEFF 4 A_Chase;
   loop;

Missile:
   FATT G 10 A_FatRaise;
   FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
   FATT HH 5 bright A_FatAttack1;
   FATT IG 6 A_FaceTarget;
   FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
   FATT HH 5 bright A_FatAttack2;
   FATT IG 6 A_FaceTarget;
   FATT H 0 A_StartSound("enemies/mancubus/fire", lch_weapon);
   FATT HH 5 bright A_FatAttack3;
   FATT IG 6 A_FaceTarget;
   FATT G 0 ResolveState("See");
   stop;

Pain:
   FATT J 3;
   FATT J 3 A_Pain;
   FATT J 0 ResolveState("See");
   stop;

Death:
   FATT K -1;
Crash:
   FATT K 6 Lith_A_Die;
   FATT LMNOPQRS 6;
   FATT T -1 A_BossDeath;
   stop;

Raise:
   FATT RQPONMLK 5;
   FATT A 0 ResolveState("See");
   stop;
}

/* EOF */
