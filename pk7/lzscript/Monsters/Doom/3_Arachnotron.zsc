/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_ArchoPlasmTrail : Actor
{
   default
   {
      Alpha       0.666;
      Height      2;
      Radius      2;
      Renderstyle "Translucent";
      Scale       0.8;

      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      ACPX A 0;
      ACPX AA 1 bright A_SpawnItemEx("Lith_ArchoPlasmParticles", -1, frandom(-1, 1), frandom(-1, 1), frandom(-1, 0), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
      ACPX AA 0 A_SpawnParticle("37732B", SPF_FULLBRIGHT | SPF_RELATIVE, 10, 5, 0, frandom(-8,8), frandom(-8,8), frandom(-2,2), frandom(0, 10), frandom(-0.1, 0.1), frandom(-1.0, 1.0), 0, 0, 0.02, 1.0, -1);
      ACPX BCDEF 2 A_FadeOut(0.2);
      stop;
   }
}

class Lith_ArchoPlasmParticles : Actor
{
   default
   {
      Alpha       0.4;
      Height      2;
      Radius      2;
      Renderstyle "Translucent";

      +DONTSPLASH
      +NOINTERACTION
   }

   states
   {
   Spawn:
      ACPT A 2 bright;
      ACPT BC 3;
      ACPT DDEE 2 A_FadeOut(0.15);
      stop;
   }
}

class Lith_ArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
   default
   {
      Damage     5;
      DamageType "Electric";
      Height     8;
      Radius     13;
      Scale      0.9;
      Speed      25;

      Projectile;
      +RANDOMIZE
   }

   states
   {
   Spawn:
      ACPL ABCDABCD 1 bright A_SpawnItemEx("Lith_ArchoPlasmTrail", -1, frandom(-1, 1), frandom(-1, 1), frandom(-1, 0), frandom(-0.5, 0.5), frandom(-0.5, 0.5));
      loop;
   Death:
      APLS A 2 bright;
      ACPX ABC 2 bright;
      ACPX DEFG 3;
      stop;
   }
}

class Lith_XH_Arachnotron : Lith_Arachnotron
{
}

class Lith_Arachnotron : Lith_Monster replaces Arachnotron;

default
{
   DamageFactor "Electric", 0.6;
   Health       500;
   Height       64;
   Mass         600;
   PainChance   128;
   Radius       64;
   Speed        12;

   ActiveSound "baby/active";
   DeathSound  "baby/death";
   PainSound   "baby/pain";
   SeeSound    "baby/sight";

   Obituary "$OB_BABY";
   Tag      "$LITH_INFO_SHORT_Arachnotron";

   +BOSSDEATH
   +FLOORCLIP
}

states
{
Spawn:
   BSPI AB 10 A_Look;
   loop;

See:
   BSPI A 20;
   BSPI A 0 ResolveState("SeeLoop");
SeeLoop:
   BSPI A 3 A_BabyMetal;
   BSPI ABBCC 3 A_Chase;
   BSPI D 3 A_BabyMetal;
   BSPI DEEFF 3 A_Chase;
   loop;

Missile:
   BSPI A 20 bright A_FaceTarget;
MissileLoop:
   BSPI G 4 bright A_BspiAttack;
   BSPI H 4 bright;
   BSPI H 1 bright A_SpidRefire;
   loop;

Pain:
   BSPI I 3;
   BSPI I 3 A_Pain;
   BSPI I 0 ResolveState("SeeLoop");
   stop;

Death:
   BSPI J -1;
Crash:
   BSPI J 20 Lith_A_Die;
   BSPI KLMNO 7;
   BSPI P -1 A_BossDeath;
   stop;

Raise:
   BSPI PONMLKJ 5;
   BSPI A 0 ResolveState("SeeLoop");
   stop;
}

/* EOF */
