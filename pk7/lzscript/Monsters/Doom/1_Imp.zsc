// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_XH_ImpBall : Lith_ImpBall
{
   default
   {
      FastSpeed 48;
      Speed 24;
   }
}

class Lith_XH_Imp : Lith_Imp
{
   default
   {
      Health 80;
      MinMissileChance 140;
      Speed 8;

      +MISSILEMORE
   }

   states
   {
   See:
      TROO AAABBBCCCDDD 1 A_Chase;
      loop;
   Melee:
   Missile:
      TROO EF 6 A_FaceTarget;
      TROO G 5 A_CustomComboAttack("Lith_XH_ImpBall", 32, 3 * random(1, 8), "imp/melee");
      TROO G 2;
      goto See;
   }
}

class Lith_ImpBall : DoomImpBall
{
   default
   {
      DamageType "Fire";
      FastSpeed 32;
      Scale 1.35;
      Speed 16;
   }

   states
   {
   Spawn:
      BAL1 AB 4 bright;
      loop;
   Death:
      BAL1 A 0 A_SetScale(1.0);
      BAL1 CDE 6 bright;
      stop;
   }
}

class Lith_Imp : Lith_Monster replaces DoomImp;

default
{
   Health           79;
   Height           50;
   Mass             100;
   MinMissileChance 160;
   Radius           18;
   Scale            0.9;
   Speed            9;

   ActiveSound "imp/active";
   DeathSound  "imp/death";
   PainSound   "imp/pain";
   SeeSound    "imp/sight";

   HitObituary "$OB_IMPHIT";
   Obituary    "$OB_IMP";
   Tag         "$LITH_INFO_SHORT_Imp";

   +FLOORCLIP
}

states
{
Spawn:
   TROO AB 10 A_Look;
   loop;

See:
   TROO AABBCCDD 2 A_Chase;
   loop;

Melee:
Missile:
   TROO EF 6 A_FaceTarget;
   TROO G 6 A_CustomComboAttack("Lith_ImpBall", 32, 3 * random(1, 8), "imp/melee");
   TROO G 2;
   goto See;

Pain:
   TROO H 2;
   TROO H 2 A_Pain;
   TROO H 0 ResolveState("See");
   stop;

Death:
   TROO I -1;
Crash:
   TROO I 8 Lith_A_Die;
   TROO J 8;
   TROO KL 6;
   TROO M -1;
   stop;
Death.Extreme:
   TROO N -1;
Crash.Extreme:
   TROO O 5 Lith_A_XDie;
   TROO PQRST 5;
   TROO U -1;
   stop;

Raise:
   TROO ML 8;
   TROO KJI 6;
   TROO A 0 ResolveState("See");
   stop;
}

/* EOF */
