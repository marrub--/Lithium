/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_JamesRevolverTrail : Actor
{
   default
   {
      Alpha       0.8;
      RenderStyle "Add";
      Scale       0.7;

      +NOINTERACTION
   }

   states
   {
   Spawn:
      PUFF A 1 A_FadeOut;
      wait;
   }
}

class Lith_JamesRevolverBullet : FastProjectile
{
   default
   {
      Alpha          0.9;
      DamageFunction 10 * Random(1, 4);
      DamageType     "Bullet";
      Height         4;
      MissileType    "Lith_JamesRevolverTrail";
      Radius         20;
      RenderStyle    "Translucent";
      Scale          0.8;
      Speed          64;

      +BLOODSPLATTER
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   Crash:
   Death:
      PUFF CD 4;
      stop;
   XDeath:
      TNT1 A 0;
      stop;
   }
}

class Lith_Boss_James : Lith_Phantom;

default
{
   Health 1000;

   Tag "James, Vanagloriam Est";
}

states
{
See:
   PLAY AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase("Melee", "Missile", m_Phase > 1 ? CHF_FASTCHASE : 0);
   loop;
Missile:
   PLAY E 0 A_JumpIf(m_Phase < 2, "Missile1");
   PLAY F 0 A_JumpIf(m_MeleeTime > 0, "Missile1");
   PLAY E 0 A_JumpIfCloser(384, "Lunge");
Missile1:
   PLAY E 0 A_StartSound("enemies/james/fire", lch_weapon);
   PLAY F 0 A_FaceTarget;
   PLAY E 5 A_SpawnProjectile("Lith_JamesRevolverBullet", 32, 0, frandom(-20, 20), CMF_OFFSETPITCH|CMF_BADPITCH, frandom(-5, 5));
   PLAY F 2 A_FaceTarget;
   PLAY F 0 A_FaceTarget;
   PLAY F 0 A_Jump(128, "See");
   PLAY F 2 A_MonsterRefire(0, "See");
   loop;
Melee:
   PLAY F 0 A_JumpIf(m_Phase < 2, "Missile");
   PLAY F 0 A_JumpIf(m_MeleeTime > 0, "Missile");
   PLAY F 0 {m_MeleeTime = 35;}
   PLAY F 0 A_FaceTarget;
   PLAY F 5 A_CustomMeleeAttack(15 * random(1, 5), "enemies/james/knifehit", "enemies/james/knife", "Melee");
   goto See;
Lunge:
   PLAY A 0 A_StartSound("enemies/phantom/lunge", lch_weapon2, attenuation: 0.5);
   PLAY A 10 A_FaceTarget;
   PLAY A 0 A_FaceTarget;
   PLAY A 5 ACS_NamedExecuteWithResult("Lith_PhantomTeleport");
   goto Melee;
}

/* EOF */
