// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_IsaacThompsonBullet : FastProjectile
{
   default
   {
      Alpha          0.9;
      DamageFunction 3 * Random(1, 4);
      DamageType     "Bullet";
      Height         4;
      MissileType    "Lith_MonsterBulletTrail";
      Radius         4;
      RenderStyle    "Translucent";
      Scale          0.3;
      Species        "Lith_Phantom";
      Speed          128;

      +BLOODSPLATTER
      +DONTHARMSPECIES
      +MTHRUSPECIES
   }

   states
   {
   Spawn:
      TNT1 A 1;
      wait;
   Crash:
   Death:
      PUFF CD 4;
      stop;
   XDeath:
      TNT1 A 0;
      stop;
   }
}

class Lith_Boss_Isaac : Lith_Phantom;

default
{
   Health 4000;

   Tag "$LITH_INFO_TITLE_Isaac";
}

override void Lith_PostInit()
{
   if(m_Phase == 3)
      A_SpawnItemEx("Lith_Boss_Michael", flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);

   Super.Lith_PostInit();
}

override void Lith_OnDeath()
{
   Super.Lith_OnDeath();
   A_KillChildren();
}

void Lith_A_SpawnDupe()
{
   if(random(0, 32) == 0 && m_Phase == 3 && m_Dupes < 3)
   {
      m_Dupes++;
      A_SpawnItemEx("Lith_Boss_Johnson", flags: SXF_SETMASTER|SXF_NOCHECKPOSITION);
   }
}

int m_Dupes;
override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("enemies/isaac/fire");
}

states
{
See:
   TNT1 A 0 Lith_A_SpawnDupe;
   PLAY AAAABBBBCCCCDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
   loop;
Missile:
   TNT1 A 0 Lith_A_SpawnDupe;
   PLAY E 0 A_StartSound("enemies/isaac/fire", lch_weapon);
   PLAY EE 0 A_SpawnProjectile("Lith_IsaacThompsonBullet", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5));
   PLAY E 1 A_FaceTarget;
   PLAY F 1 A_FaceTarget;
   PLAY F 0 A_Chase(null, null, CHF_FASTCHASE);
   PLAY F 2 A_MonsterRefire(0, "See");
   loop;
}

/* EOF */
