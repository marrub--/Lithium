/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_IsaacThompsonBullet2 : Lith_IsaacThompsonBullet {default {Damage 1;}}

class Lith_Boss_Johnson : Lith_BasicPhantom;

default
{
   Health 40;
   Speed  3;

   Tag "$LITH_INFO_TITLE_Isaac";

   +THRUSPECIES
   +NOINFIGHTING
}

override void Tick()
{
   Super.Tick();
   if(Health < 20) SetTag("$LITH_INFO_TITLE_Johnson");
}

override void Lith_OnDeath()
{
   let m = Lith_Boss_Isaac(Master);
   if(m) m.m_Dupes--;
}

states
{
See:
   PLAY AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
   loop;
Missile:
   PLAY E 0 A_StartSound("enemies/isaac/fire", lch_weapon);
   PLAY EE 0 A_SpawnProjectile("Lith_IsaacThompsonBullet2", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5));
   PLAY E 1 A_FaceTarget;
   PLAY F 1 A_FaceTarget;
   PLAY F 2 A_MonsterRefire(0, "See");
   loop;
Death:
   PLAY H 10;
   PLAY I 1 A_FadeOut(0.01);
   wait;
}

/* EOF */
