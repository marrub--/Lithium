// Distributed under the CC0 public domain license.
// By Alison Sanderson. Attribution is encouraged, though not required.
// See licenses/cc0.txt for more information.

class Lith_IsaacThompsonBullet2 : Lith_IsaacThompsonBullet {default {Damage 1;}}

class Lith_Boss_Johnson : Lith_BasicPhantom;

default
{
   Health 40;
   Speed  3;

   Tag "Isaac, Superbiorem Est";

   +THRUSPECIES
   +NOINFIGHTING
}

override void Tick()
{
   Super.Tick();
   if(Health < 20) SetTag("Johnson, Ally of Isaac");
}

override void Lith_OnDeath()
{
   let m = Lith_Boss_Isaac(Master);
   if(m) m.m_Dupes--;
}

states
{
See:
   PLAY AAAAAAAABBBBBBBBCCCCCCCCDDDDDDDD 1 A_Chase(null, "Missile", CHF_FASTCHASE);
   loop;
Missile:
   PLAY E 0 A_PlaySound("enemies/isaac/fire", CHAN_WEAPON);
   PLAY EE 0 A_SpawnProjectile("Lith_IsaacThompsonBullet2", 32, 0, frandom(-10, 10), CMF_OFFSETPITCH, frandom(-5, 5));
   PLAY E 1 A_FaceTarget;
   PLAY F 1 A_FaceTarget;
   PLAY F 2 A_MonsterRefire(0, "See");
   loop;
Death:
   PLAY H 10;
   PLAY I 1 A_FadeOut(0.01);
   wait;
}

// EOF
