/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_RiotShotgun : Lith_TWeapon;

const _ammo_used = 10;

default {
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_RiotShotgun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 1;

   +WEAPON.NOAUTOFIRE;
}

bool m_duplex;

action(Weapon) void Lith_A_FireRiotShotgun() {
   A_TakeInventory("Lith_Mana", _ammo_used, TIF_NOTAKEINFINITE);
   A_GunFlash("DummyFlash");
   Lith_FireBullets(7, 7, 17, 7, FBF_NORANDOM);
   Lith_A_GunSmoke();
   Lith_A_RecoilUp(4);
   A_StartSound("weapons/riotshot/fire", lch_weapon);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

states(Weapon) {
Ready:
   PISG A 0 {invoker.m_duplex = false;}
   PISG A 1 Lith_A_Ready;
   loop;

Deselect:
   PISG A 1 A_Lower(12);
   loop;

Select:
   PISG A 1 A_Raise(12);
   loop;

Fire:
   PISG A 0 A_JumpIf(CountInv("Lith_Mana") < _ammo_used, "FireEmpty");
   PISG A 5 Lith_A_FireRiotShotgun;
   PISG A 0 A_JumpIf(invoker.m_duplex, "ReadySelector");
FireWait:
   PISG A 1;
   PISG A 1 A_ReFire("FireWait");
   PISG A 0 {invoker.m_duplex = true;}
   goto Fire;
FireEmpty:
   #### # 1;
   #### # 2 offset(0, 41);
   #### # 1 offset(0, 45);
   #### # 1 offset(0, 43);
   #### # 1 offset(0, 40) A_StartSound("weapons/rocket/empty", lch_dryweapon);
   #### # 1 offset(0, 38);
   #### # 1 offset(0, 36);
   #### # 1 offset(0, 33);
   #### # 1 offset(0, 32);
   #### # 5;
   #### #### 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
