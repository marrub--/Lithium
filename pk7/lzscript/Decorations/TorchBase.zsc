// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_TorchDecor : Lith_FlareDecor {
   default {
      Radius 12;
      Lith_SwitchableDecor.Noise "amb/fire";
   }

   meta class<Actor> m_Type1;
   meta color m_Col1, m_Col2, m_Col3;
   meta double m_Height;
   property Height: m_Height;
   property TypeColo: m_Type1, m_Col1, m_Col2, m_Col3;

   void Lith_A_SpawnFlame() {
      bool less = lith_sv_lessparticles;
      if(less && random[lith_torchbase](0, 1)) return;
      A_SpawnItemEx(m_Type1,
         frandom(-4,4), frandom(-4,4), frandom(-0.5,0.5) + m_Height,
         frandom(-0.1,0.1), frandom(-0.1,0.1), frandom(0,2.0));
      if(less) return;
      A_SpawnParticle(m_Col1, SPF_FULLBRIGHT | SPF_RELATIVE, 25, 2, 0,
         frandom(-8,8), frandom(-8,8), frandom(-1,5) + m_Height-5,
         frandom(-0.1,0.1), frandom(-0.1,0.1), frandom(0,2.0),
         0, 0, 0.02,
         1.0, -1);
      A_SpawnParticle(m_Col2, SPF_FULLBRIGHT | SPF_RELATIVE, 15, 2, 0,
         frandom(-4,4), frandom(-4,4), frandom(-1,2) + m_Height-5,
         frandom(-0.1,0.1), frandom(-0.1,0.1), frandom(0,2.0),
         0, 0, 0.02,
         1.0, -1);
      A_SpawnParticle(m_Col3, SPF_FULLBRIGHT | SPF_RELATIVE, 5, 2, 0,
         frandom(-2,2), frandom(-2,2), frandom(-1,2) + m_Height-5,
         frandom(-0.1,0.1), frandom(-0.1,0.1), frandom(0,2.0),
         0, 0, 0.02,
         1.0, -1);
   }

   override void markPrecacheSounds() {
      super.markPrecacheSounds();
      markSound("amb/woodx");
   }

   states {
   Inactive:
      #### A -1;
      stop;
   Death:
      #### X 0 A_StartSound("amb/woodx", lch_body3);
      #### X -1;
      stop;
   }
}

class Lith_TorchFlame : Actor {
   default {
      Radius 2;
      Height 2;
      Alpha 0.25;
      Renderstyle "Translucent";
      +DONTSPLASH
      +NOINTERACTION
      +BRIGHT
   }

   states {
   Spawn1:
      #### # 4;
      #### # 0 {++frame;}
      #### # 4;
      #### # 0 {++frame;}
      #### # 2;
      #### # 0 {++frame;}
      #### # 2;
      #### # 0 {
         if(lith_sv_lessparticles) {
            destroy();
         }
      }
      #### # 0 {++frame;}
      #### ## 1 a_fadeOut(0.025);
      #### # 0 {++frame;}
      #### ## 1 a_fadeOut(0.025);
      #### # 0 {++frame;}
      #### ## 1 a_fadeOut(0.05);
      stop;
   }
}

class Lith_TorchRFlame : Lith_TorchFlame {states {Spawn: LI05 C 0; goto Spawn1;}}
class Lith_TorchGFlame : Lith_TorchFlame {states {Spawn: LI05 J 0; goto Spawn1;}}
class Lith_TorchBFlame : Lith_TorchFlame {states {Spawn: LI05 Q 0; goto Spawn1;}}

/* EOF */
