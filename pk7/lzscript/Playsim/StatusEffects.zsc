// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

extend class Lith_HERMES {
   static void addStatFx(string which) {
      Lith_StatFx.addToPool(which);
   }

   static bool hasStatFx(string which) {
      return Lith_StatFx.getInPool(which) != null;
   }

   static void remStatFx(string which) {
      Lith_StatFx.deleteFromPool(which);
   }
}

extend class Lith_URANUS {
   ui int drawStatFx(RenderEvent evt, int y) {
      Lith_StatFx stat;
      int hudlpos = Lith_UTIL.pData(_pdt_hudlpos);
      let fnt = fntSml();
      int i = 0;
      for(let it = ThinkerIterator.create("Lith_StatFx", _stat_statfx);
          (stat = Lith_StatFx(it.next()));)
      {
         let s = Lith_HERMES.localize(stat.fnam);
         let a = (sin((gameTic + evt.fracTic + i++ * 10) * 7.0) + 1.5) / 3.0;
         int x = hudlpos;

         Screen.drawTexture(stat.icon, false, x, y,
                            DTA_VirtualWidth,320, DTA_VirtualHeight,240);
         x += 12;
         y += 2;

         Screen.drawText(fnt, Font.CR_WHITE, x, y, s,
                         DTA_VirtualWidth,320, DTA_VirtualHeight,240);
         x += fnt.stringWidth(s) + 4;

         if(stat.cnt0 != 0) {
            let m = "x" .. stat.cnt0;
            Screen.drawText(fnt, Font.CR_WHITE, x, y, m,
                            DTA_VirtualWidth,320, DTA_VirtualHeight,240);
            x += fnt.stringWidth(m) + 4;
         }

         if(stat.tics >= 0) {
            let tics = stat.tics - evt.fracTic;
            let mins = tics / 35.0 / 60.0;
            let secs = tics / 35.0 % 60.0;
            let t = string.format("%i:%04.1f", int(mins), secs);
            Screen.drawText(fnt, Font.CR_SAPPHIRE, x, y, t,
                            DTA_VirtualWidth,320, DTA_VirtualHeight,240,
                            DTA_LegacyRenderStyle,STYLE_TRANSLUCENT,
                            DTA_Alpha,a);
         }
         y += 9;
      }
      return y;
   }
}

class Lith_StatFx : Thinker abstract;

string      snam;
string      fnam;
textureId   icon;
int         tics;
int         cnt0;
Lith_Player plyr;

static Lith_StatFx addToPool(class<Lith_StatFx> which) {
   let plyr = Lith_Player(players[consolePlayer].mo);
   Lith_StatFx stat;
   for(let it = ThinkerIterator.create("Lith_StatFx", _stat_statfx);
       (stat = Lith_StatFx(it.next()));)
   {
      if(stat is which) {
         stat.init(stat.plyr);
         return stat;
      }
   }
   stat = Lith_StatFx(new(which));
   stat.init(plyr);
   return stat;
}

static Lith_StatFx getInPool(class<Lith_StatFx> which) {
   Lith_StatFx stat;
   for(let it = ThinkerIterator.create("Lith_StatFx", _stat_statfx);
       (stat = Lith_StatFx(it.next()));)
   {
      if(stat is which) {
         return stat;
      }
   }
   return null;
}

static void deleteFromPool(class<Lith_StatFx> which) {
   Lith_StatFx stat;
   for(let it = ThinkerIterator.create("Lith_StatFx", _stat_statfx);
       (stat = Lith_StatFx(it.next()));)
   {
      if(stat is which) {
         stat.destroy();
         return;
      }
   }
}

void basic(string snam_, int tics_) {
   snam = snam_;
   tics = tics_;
}

void give(class<Inventory> which, int n = 1) {
   plyr.giveInventory(which, n);
}

void take(class<Inventory> which, int n = int.MAX) {
   if(plyr) plyr.takeInventory(which, n);
}

void init(Lith_Player plyr_) {
   changeStatNum(_stat_statfx);
   plyr = plyr_;
   initCb();
   fnam = "LITH_STAT_" .. snam;
   icon = Lith_URANUS.getTex(":Stat:" .. snam);
}

override void tick() {
   if(level.isFrozen()) return;
   tickCb();
   if(tics > 0) {
      --tics;
   } else if(tics == 0) {
      destroy();
   }
}

override void onDestroy() {
   if(plyr) {
      doneCb();
   }
}

virtual void initCb() {}
virtual void tickCb() {}
virtual void doneCb() {}

/* EOF */
