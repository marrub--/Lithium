// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_SubAxe : Actor {
   default {
      Damage 30;
      DamageType "Magic";
      Speed 16;
      Projectile;
      +Bright
   }
   override int specialMissileHit(Actor th) {
      if(th != target) {
         if(th) {
            int dmg = getMissileDamage(3, 1);
            if(target) {
               target.acs_scriptCall("Lith_GiveHealth", dmg / 2);
            }
            th.damageMobj(self, target, dmg, 'Magic');
            for(int i = 0; i < 10; ++i) {
               a_startSound("body/flesh", lch_auto);
            }
            a_startSound("weapons/kampilan/hitflesh", lch_weapon2);
         }
         return 0;
      } else {
         return 1;
      }
   }
   states {
   Spawn:
      DAXE ABC 3;
      loop;
   Death:
      DAXE DDEEFF 3 a_fadeOut;
      stop;
   }
}

class Lith_SubDart : Actor {
   default {
      Damage 0;
      DamageType "Poison";
      Speed 16;
      Radius 2;
      Projectile;
      +Bright
   }
   int    m_dartTicks;
   double m_dartAngle;
   double m_dartZ;
   override int specialMissileHit(Actor th) {
      if(th != target) {
         if(th) {
            vector2 p = th.pos.xy - pos.xy;
            tracer = th;
            m_dartAngle = Lith_UTIL.constDeg(atan2(p.y, p.x) - 180.0);
            m_dartZ     = pos.z - th.pos.z;
            a_startSound("body/flesh", lch_auto);
         }
         return 0;
      } else {
         return 1;
      }
   }
   override void tick() {
      super.tick();
      if(m_dartTicks && tracer) {
         a_warp(AAPTR_TRACER, 0, 0, m_dartZ, m_dartAngle, WARPF_COPYINTERPOLATION|WARPF_NOCHECKPOSITION, radiusOffset: 1);
         angle = atan2(tracer.pos.y - pos.y, tracer.pos.x - pos.x);
      }
   }
   states {
   Spawn:
      DDAR A 3;
      loop;
   Death:
      DDAR A 35 {
         if(tracer && m_dartTicks++ < 10) {
            tracer.a_damageSelf(30, "Poison");
         } else {
            destroy();
         }
      }
      loop;
   }
}

class Lith_DWeapon : Lith_Weapon;

default {
   Lith_Weapon.SprintSpeed 2.1;
   Inventory.RestrictedTo "Lith_DarkLordPlayer";
   Weapon.BobStyle "InverseAlpha";
   Weapon.BobSpeed 0.9;
   +Weapon.NoAutoFire
}
override bool lith_checkSprint() {
   return owner.getPlayerInput(INPUT_BUTTONS) & BT_SPEED != 0;
}
action(Weapon) void lith_subGunPreFire() {
   player.getPSprite(_psp_d_subweapon).frame = randomPick(1, 3);
}
action(Weapon) void lith_subGunFire() {
   player.getPSprite(_psp_d_subweapon).frame = randomPick(0, 2);
   a_startSound("weapons/subweapon/armgun", lch_altweapon);
   a_alertMonsters();
   lith_fireBullets(1, 2, -1, 15 * random(2, 4), flags: FBF_NORANDOM, tracerX: -6);
   A_FireProjectile("Lith_LightProjectileRedShort", 0);
}
action(Weapon) void lith_subGrnFire() {
   a_startSound("weapons/subweapon/grenade", lch_altweapon);
   a_alertMonsters();
   a_fireProjectile("Lith_RifleGrenade", 0, false, -7, 6);
}
action(Weapon) void lith_subAxeFire() {
   a_startSound("weapons/subweapon/axe", lch_altweapon);
   a_alertMonsters();
   a_fireProjectile("Lith_SubAxe", 0, false, -7, 6);
}
action(Weapon) void lith_subDrtFire() {
   a_startSound("weapons/subweapon/needle", lch_altweapon);
   a_fireProjectile("Lith_SubDart", 0, false, -7, 6);
}
override void tick() {
   super.tick();
   if(owner && owner.player && owner.health > 0 && !isFrozen() &&
      owner.player.buttons & BT_ALTATTACK &&
      owner.player.getPSprite(_psp_d_subweapon).tics <= 0 &&
      owner.acs_scriptCall("Lith_GetSubShots") > 0) {
      owner.acs_scriptCall("Lith_TakeSubShot");
      state which;
      switch(owner.acs_scriptCall("Lith_GetSubType")) {
      case _subw_gun:     which = resolveState("_SubGun"); break;
      case _subw_grenade: which = resolveState("_SubGrn"); break;
      case _subw_axe:     which = resolveState("_SubAxe"); break;
      case _subw_dart:    which = resolveState("_SubDrt"); break;
      }
      owner.player.setPSprite(_psp_d_subweapon, which);
      let psp = owner.player.getPSprite(_psp_d_subweapon);
      psp.bAddWeapon = false;
      psp.bAddBob    = false;
      psp.bPowDouble = true;
      psp.bCVarFast  = true;
   }
}
action(Weapon) void lith_sprintOfs(double x, double y, int flags = 0) {
   lith_sprint();
   a_weaponOffset(x, y, flags);
}
states(Weapon) {
_Sprint:
   #### # 1 offset(1, 34) lith_a_cancelScope;
   #### # 1 offset(4, 40);
   #### # 1 offset(10, 55) a_startSound("player/ari/sprint", lch_body3);
   #### # 1 offset(17, 70);
   #### # 1 offset(21, 90);
   #### # 1 offset(24, 120);
   #### # 1 offset(25, 160) lith_sprint;
   TNT1 A 1 {
      lith_sprint();
      a_weaponOffset(0, 32);
      a_overlay(_psp_double_lg, "Null");
      a_overlay(_psp_double_rg, "Null");
   }
_SprintLoop:
   DSPG A 0 lith_jumpIfSprint;
   DSPG # 1 lith_sprintOfs(-18+fRandom[lith_weapon](-4, 0), 45);
   DSPG # 1 lith_sprintOfs(9, -10, wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(7, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(1, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(0, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(2,  0,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(2,  3,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(4,  4,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(7,  5,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(6,  1,  wof_interpolate|wof_add);
   DSPG B 0 lith_jumpIfSprint;
   DSPG # 1 lith_sprintOfs(18+fRandom[lith_weapon](0, 4), 45);
   DSPG # 1 lith_sprintOfs(-9, -10, wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-7, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-1, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs( 0, -1,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-2,  0,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-2,  3,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-4,  4,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-7,  5,  wof_interpolate|wof_add);
   DSPG # 1 lith_sprintOfs(-6,  1,  wof_interpolate|wof_add);
   loop;
_SprintDone:
   TNT1 A 1 offset(1, 32) a_lower(60);
   TNT1 A 0 resolveState("Select");
   stop;
_SubGun:
   ARGN B 1 bright lith_subGunPreFire;
   ARGN A 1 bright lith_subGunFire;
   TNT1 A 1;
   ARGN B 1 bright lith_subGunPreFire;
   ARGN A 1 bright lith_subGunFire;
   TNT1 A 1;
   ARGN B 1 bright lith_subGunPreFire;
   ARGN A 1 bright lith_subGunFire;
   TNT1 A 1;
   ARGN B 1 bright lith_subGunPreFire;
   ARGN A 1 bright lith_subGunFire;
   TNT1 A 1;
   ARGN B 1 bright lith_subGunPreFire;
   ARGN A 1 bright lith_subGunFire;
   TNT1 A 4;
   stop;
_SubGrn:
   AGRN A 1 bright lith_subGrnFire;
   AGRN BCDEFG 1 bright;
   TNT1 A 11;
   stop;
_SubAxe:
   AAXE A 2 bright lith_subAxeFire;
   AAXE BCD 2 bright;
   TNT1 A 10;
   stop;
_SubDrt:
   TNT1 A 18 lith_subDrtFire;
   stop;
}

/* EOF */
