// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_ShieldBlast : Actor {
   default {
      Radius 16;
      Height 16;
      Speed 32;
      Damage 30;
      RenderStyle "Add";
      SeeSound "weapons/plasdiff/fire";
      DeathSound "weapons/plasdiff/explode";
      XScale 0.5;
      Projectile;
      +FloorHugger
   }
   states {
   Spawn:
      BLLT N 35 bright;
      BLLT N 1 bright a_fadeOut(0.01);
      wait;
   Death:
      BLLT N 1 bright a_fadeOut(0.1);
      wait;
   }
}

class Lith_DarkLordPlayer : Lith_Player {
   default {
      Health 150;
      Mass 110;
      Gravity 1.05;
      Lith_Player.ViewBob 0.4;
      Player.AttackZOffset 17;
      Player.DisplayName "$Lith_DarkLordPlayer";
      Player.MaxHealth 150;
      Player.SoundClass "lith_darklord";
      Player.Portrait ":Portraits:DarkLord";
      Player.ViewHeight 50;
      Tag "$Lith_DarkLordPlayer";
   }
   override void beginPlay() {
      super.beginPlay();
      m_discrim = "Ari";
      m_pclass = pcl_darklord;
   }
   override void giveDefaultInventory() {
      super.giveDefaultInventory();
      lith_giveDefWeapon("Lith_Khanda");
      lith_giveDefWeapon("Lith_700Express");
   }
   override void tick() {
      if(!isFrozen()) {
         if(vel.z < 0 && vel.z > -128 && player.cmd.buttons & BT_CROUCH) {
            vel.z -= 2 * (level.gravity / 800) * gravity * curSector.gravity;
         }
         if(Lith_UTIL.pData(_pdt_upgrade, UPGR_SMT)) {
            tickNegotiation();
         }
      }
      super.tick();
   }
   override void playerLandedMakeGruntSound(Actor landedOn) {
      let wep = Lith_Weapon(player.readyWeapon);
      if(wep && Lith_UTIL.pData(_pdt_upgrade, UPGR_Rems_A) &&
         wep.m_ads != Lith_Weapon._ads_none)
      {
         a_startSound("weapons/rems/clonk", lch_body3, CHANF_OVERLAP);
      }
      super.playerLandedMakeGruntSound(landedOn);
   }
}

#include "lzscript/6_DarkLord/Negotiation.zsc"
#include "lzscript/6_DarkLord/0_Weapon.zsc"
#include "lzscript/6_DarkLord/1_Kampilan.zsc"
#include "lzscript/6_DarkLord/1_Khanda.zsc"
#include "lzscript/6_DarkLord/2_700Express.zsc"
#include "lzscript/6_DarkLord/3_4Bore.zsc"
#include "lzscript/6_DarkLord/3_ShrapnelGun.zsc"
#include "lzscript/6_DarkLord/4_Minigun.zsc"
#include "lzscript/6_DarkLord/4_Motra.zsc"
#include "lzscript/6_DarkLord/5_DualRocket.zsc"
#include "lzscript/6_DarkLord/6_FortuneGun.zsc"
#include "lzscript/6_DarkLord/7_Rems.zsc"

/* EOF */
