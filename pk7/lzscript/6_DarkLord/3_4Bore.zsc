/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison G. Watson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_4Bore : Lith_DWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_SuperShotgunPickup";
   Tag "$LITH_INFO_SHORT_4Bore";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/4bore/draw";
   Lith_Weapon.AmmoType "Lith_ShellAmmo";
   Inventory.AltHudIcon "4BORZ0";
}

action(Weapon) void Lith_A_Fire4Bore()
{
   A_AlertMonsters();
   lith_takeAmmo(8);
   A_SetBlend("White", 0.2, 6);
   A_GunFlash("DummyFlash");
   Lith_A_GunSmoke(2, 8);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
   Lith_FireBullets(4, 2, 8, 50 * random(1, 3), FBF_NORANDOM, "Lith_700Bullet", tracer: _tr_big);
   Lith_A_RecoilUp(10);
}

states(Weapon)
{
Ready:
   4BOR A 1 Lith_A_Ready;
   loop;

Deselect:
   4BOR A 1 Lith_A_Lower(_raise_normal);
   loop;
Select:
   4BOR A 1 Lith_A_Raise(_raise_normal);
   loop;

Fire:
   #### # 0 lith_jumpNoAmmo(8);
   #### # 0 lith_a_setInterpolation(true);
   4BOR A 7 A_StartSound("weapons/4bore/fire", lch_weapon);
   4BOR G 1 offset(140, 121) Lith_A_Fire4Bore;
   4BOR H 1 bright offset(89, 71);
   4BOR B 1 bright offset(97, 77);
   4BOR G 1 offset(156, 135);
   4BOR A 1 offset(15, 48);
   4BOR A 1 offset(11, 44);
   4BOR A 1 offset(8, 41);
   4BOR A 2 offset(5, 39);
   4BOR A 1 offset(3, 36);
   4BOR A 1 offset(2, 34);
   4BOR A 1 offset(-3, 32);
   4BOR A 1 offset(-5, 32);
   4BOR A 1 offset(-10, 33);
   4BOR A 1 offset(-22, 36);
   4BOR C 1 offset(19, 130);
   4BOR C 1 offset(4, 135);
   4BOR C 1 offset(-22, 143);
   4BOR D 1 offset(-28, 166) {
      A_StartSound("weapons/ssg/open", lch_reload);
      Lith_A_Casing("Lith_CasShellHot", 15, -1, 40, FRandom(3,4), FRandom(7,8), FRandom(2,9));
      Lith_A_Casing("Lith_CasShellHot", 15, -4, 40, FRandom(3,4), FRandom(7,8), FRandom(2,9));
   }
   4BOR D 1 offset(-41, 172);
   4BOR D 1 offset(-56, 186);
   4BOR D 1 offset(-71, 201);
   TNT1 A 14 A_StartSound("weapons/ssg/load", lch_reload2);
   4BOR F 1 offset(-43, 182) Lith_A_ReloadBobUp(1);
   4BOR F 1 offset(-25, 155);
   4BOR F 1 offset(-8, 137);
   4BOR E 3 offset(55, 96) A_StartSound("weapons/ssg/close", lch_reload3);
   4BOR E 2 offset(59, 95);
   4BOR E 2 offset(60, 97);
   4BOR E 2 offset(60, 95);
   4BOR E 3 offset(61, 98);
   4BOR E 6 offset(63, 96);
   #### # 0 lith_a_setInterpolation(false);
   4BOR C 1 offset(-2, 112);
   4BOR C 1 offset(11, 115);
   4BOR A 1 offset(-27, 32);
   4BOR A 1 offset(-4, 35);
   4BOR A 1 offset(5, 33);
   goto ReadySelector;
FireEmpty:
   4BOR A 2 offset(3, 38);
   4BOR A 1 offset(7, 40);
   4BOR A 1 offset(5, 37) A_StartSound("weapons/spas/empty", lch_dryweapon);
   4BOR A 1 offset(4, 34);
   4BOR A 1 offset(2, 33);
   4BOR A 5 offset(1, 32);
   goto ReadySelector;
}

/* EOF */
