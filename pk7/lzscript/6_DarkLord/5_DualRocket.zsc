// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_Franca : Lith_Rocket {
   default {
      Speed 30;
      Damage 50;
      Translation "Lith_Franca";
   }
}

class Lith_Sophia : Lith_Rocket {
   default {
      Speed 30;
      Damage 30;
      Translation "Lith_Sophia";
      +SeekerMissile
   }
   states {
   SpawnLoop:
      #### # 1 bright {
         Lith_A_SpawnRocketTrail();
         A_SeekerMissile(0, 2, SMF_LOOK);
      }
      loop;
   }
}

class Lith_DualRocket : Lith_DWeapon;

default {
   Lith_Weapon.SpawnType "Lith_RocketLauncherPickup";
   Tag "$LITH_INFO_SHORT_DualRocket";
   Weapon.SlotNumber 5;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/dualrocket/draw";
   Lith_Weapon.AmmoType "Lith_RocketAmmo";
   Inventory.AltHudIcon "FRANZ0";
}

action(Weapon) void Lith_A_FireRocket(class<Actor> cl, double xofs) {
   A_GunFlash("DummyFlash");
   A_FireProjectile(cl, 0, true, xofs, -2);
   lith_takeAmmo();
   Lith_A_RecoilUp(1.2);
   a_recoil(0.5);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

action(weapon) void lith_a_setupFlashOverlay(int psp, stateLabel label) {
   a_overlay(psp, label);
   a_overlayFlags(psp, pspf_renderstyle, true);
   a_overlayRenderStyle(psp, style_add);
}

states(Weapon) {
Ready:   TNT1 A 1 Lith_A_Ready;       wait;
ReadyLH: FRAN A 1 Lith_A_DoubleReady; wait;
ReadyRH: SOPH A 1 Lith_A_DoubleReady; wait;

Deselect:
   TNT1 A 1 Lith_A_Lower(_raise_normal);
   loop;

Select:
   TNT1 A 0 Lith_A_DoubleRaise;
   TNT1 A 1 Lith_A_Raise(_raise_normal);
   wait;

Fire:
   TNT1 A 0 lith_jumpNoAmmo();
   TNT1 A 10 Lith_A_DoubleState(_oscillate, "FireLH", "FireRH");
   TNT1 A 0 A_ReFire;
   goto ReadySelector;
FireLH:
   FRAN A 0 lith_a_setupFlashOverlay(_psp_double_lf, "FlashLH");
   FRAN A 0 A_StartSound("weapons/dualrocket/firel", lch_weapon);
   FRAN A 1 Lith_A_FireRocket("Lith_Franca", -14);
   goto FireOffsetsLH;
FireRH:
   SOPH A 0 lith_a_setupFlashOverlay(_psp_double_rf, "FlashRH");
   SOPH A 0 A_StartSound("weapons/dualrocket/firer", lch_weapon2);
   SOPH A 1 Lith_A_FireRocket("Lith_Sophia", 14);
   goto FireOffsetsRH;
FlashLH:
   FRAN BCD 3 bright;
   stop;
FlashRH:
   SOPH BCD 3 bright;
   stop;
FireEmptyLH:
   FRAN A 0;
FireOffsetsLH:
   #### # 2 Lith_A_DoubleOffset(_lhs, -48, 48, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -38, 38, WOF_INTERPOLATE);
   #### # 2 Lith_A_DoubleOffset(_lhs, -24, 24, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -6,  11, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -3,  8,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -2,  6,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -2,  4,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs, -1,  1,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_lhs,  0,  0,  WOF_INTERPOLATE);
   goto ReadyLH;
FireEmptyRH:
   SOPH A 0;
FireOffsetsRH:
   #### # 2 Lith_A_DoubleOffset(_rhs, 48, 48, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 38, 38, WOF_INTERPOLATE);
   #### # 2 Lith_A_DoubleOffset(_rhs, 24, 24, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 5,  11, WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 3,  8,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 2,  6,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 2,  4,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 1,  1,  WOF_INTERPOLATE);
   #### # 1 Lith_A_DoubleOffset(_rhs, 0,  0,  WOF_INTERPOLATE);
   goto ReadyRH;
FireEmpty:
   #### # 6 Lith_A_DoubleState(_oscillate, "FireEmptyLH", "FireEmptyRH");
   #### # 9 A_StartSound("weapons/rocket/empty", lch_dryweapon);
   #### #### 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
