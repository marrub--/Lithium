// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_StatShadowWalker : Lith_StatFx {
   override void initCb() {basic("SWR", 105);}
   override void doneCb() {addToPool("Lith_StatShadowWalked");}
   override void tickCb() {
      let a = plyr.angle - atan2(plyr.player.cmd.sidemove,
                                 plyr.player.cmd.forwardmove);
      double speed = 1.0 + Lith_UTIL.pData(_pdt_attr, at_spc) / 30.0;
      let c = cos(a) * 0.7 * speed;
      let s = sin(a) * 0.7 * speed;
      plyr.a_changeVelocity(c, s);
      for(int i = 0; i < 4; ++i) {
         Actor
            .spawn("Lith_ShadowWalkEffect",
                   plyr.pos + (frandom(-1,1),frandom(-1,1),frandom(-1,1)))
            .vel = (frandom(-0.8,0.8),frandom(-0.8,0.8),frandom(-0.4,0.4));
      }
   }
}

class Lith_StatShadowWalked : Lith_StatFx {
   override void initCb() {basic("SWD", 525);}
}

class Lith_WandererPlayer : Lith_Player {
   default {
      Player.DisplayName "$Lith_WandererPlayer";
      Player.SoundClass "lith_wanderer";
      Player.Portrait ":Portraits:Wanderer";
      Health 50;
      Tag "$Lith_WandererPlayer";
   }

   override void beginPlay() {
      super.beginPlay();
      m_discrim = "Luke";
      m_pclass = pcl_wanderer;
   }

   override void giveDefaultInventory() {
      super.giveDefaultInventory();
      lith_giveDefWeapon("Lith_Reverse");
      lith_giveDefWeapon("Lith_Whip");
      lith_giveDefWeapon("Lith_TwoShot");
      lith_giveDefWeapon("Lith_Magnum");
   }

   override void tick() {
      if(health > 0) {
         if(!Lith_StatFx.getInPool("Lith_StatShadowWalker") &&
            !Lith_StatFx.getInPool("Lith_StatShadowWalked") &&
            player && player.buttons & BT_SPEED)
         {
            A_StartSound("player/luke/shadowwalk", lch_voice);
            Lith_StatFx.addToPool("Lith_StatShadowWalker");
            callACS("Lith_DodgeViewShadowWalk");
         }
      }

      super.tick();
   }
}

class Lith_WWeapon : Lith_Weapon {
   default {
      Inventory.RestrictedTo "Lith_WandererPlayer";
   }
}

class Lith_ShadowWalkEffect : Actor {
   default {
      Alpha 0.7;
      RenderStyle "Subtract";
      Scale 0.5;

      +NOINTERACTION
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      scale.x *= frandom(0.5, 1.5);
      scale.y *= frandom(0.5, 1.5);
      alpha *= frandom(0.5, 1.5);
   }

   states {
   Spawn:
      SWLK A random(15, 20);
      SWLK A 1 A_FadeOut;
      wait;
   }
}

#include "lzscript/4_Wanderer/1_Whip.zsc"
#include "lzscript/4_Wanderer/1_Reverse.zsc"
#include "lzscript/4_Wanderer/1_Zerstreut.zsc"
#include "lzscript/4_Wanderer/2_Magnum.zsc"
#include "lzscript/4_Wanderer/2_TwoShot.zsc"
#include "lzscript/4_Wanderer/3_DoubleBarrel.zsc"
#include "lzscript/4_Wanderer/3_CircleSnipe.zsc"
#include "lzscript/4_Wanderer/4_AlienRifle.zsc"
#include "lzscript/4_Wanderer/4_MarathonRifle.zsc"
#include "lzscript/4_Wanderer/5_TriLauncher.zsc"
#include "lzscript/4_Wanderer/6_FusionRifle.zsc"
#include "lzscript/4_Wanderer/7_Funnel.zsc"

/* EOF */
