// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_DoubleBarrel : Lith_WWeapon;

default {
   Lith_Weapon.SpawnType "Lith_ShotgunPickup";
   Lith_Weapon.MagSize 3;
   Tag "$LITH_INFO_SHORT_DoubleBarrel";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 1;
   Lith_Weapon.AmmoType "Lith_ShellAmmo";

   +Lith_Weapon.HitScan
}

action(Weapon) void Lith_A_FireDoubleBarrel()
{
   lith_takeAmmo();
   Lith_A_Casing("Lith_CasShellHot", 30, 6, 20, FRandom(3,4), FRandom(7,8), FRandom(7,9));
   Lith_A_MagIncr();
   SoundAlert(Invoker.Owner);
   A_Recoil(0.7);
   Lith_A_RecoilUp(0.4);
   A_Light(1);
   Lith_FireBullets(10.5, 3.5, 11, 7);
   Lith_A_GunSmokeAndSparks();
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
   A_StartSound("weapons/spas/fire", lch_weapon);
}

states(Weapon) {
Ready:
   CSHG A 1 lith_a_ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   CSHG A 1 Lith_A_Lower(_raise_normal);
   wait;
Select:
   CSHG A 1 Lith_A_Raise(_raise_normal);
   wait;

Fire:
   CSHG A 0 lith_jumpNoAmmo;
   CSHG A 0 Lith_A_JumpIfMagEmpty(false);
   CSHG B 1 bright offset(0, 42) Lith_A_FireDoubleBarrel;
   CSHG B 2 bright offset(0, 47);
   CSHG C 2 bright offset(0, 48) {A_Light(2); Lith_A_RecoilDn(0.2);}
   CSHG C 2 bright offset(0, 46);
   CSHG D 2 offset(0, 38) A_Light(0);
   CSHG D 2 offset(0, 35);
   goto ReadySelector;
FireEmpty:
   CSHG A 1;
   CSHG A 2 offset(0, 38);
   CSHG A 1 offset(0, 40);
   CSHG A 1 offset(0, 37) A_StartSound("weapons/spas/empty", lch_dryweapon);
   CSHG A 1 offset(0, 34);
   CSHG A 1 offset(0, 33);
   CSHG A 5 offset(0, 32);
   goto ReadySelector;

Reload:
   CSHG A 0 Lith_A_JumpIfMagFull;
   CSHR ABCDE 1;
   CSHR S 2 {
      A_StartSound("weapons/spas/pump", lch_reload2);
      Lith_A_ReloadFinish();
      Lith_A_ReloadBobDn(1.5);
   }
   CSHR XYZB 2;
   goto ReadySelector;
}

/* EOF */
