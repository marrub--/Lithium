// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_InformantPlayer : Lith_Player
{
   default
   {
      Health 50;
      Lith_Player.ViewBob 0.1;
      Player.DisplayName "$Lith_InformantPlayer";
      Player.SoundClass "lith_informant";
      Player.Portrait ":Portraits:Informant";
      Player.ViewHeight 44;
      Player.AttackZOffset 11;
      Tag "$Lith_InformantPlayer";
      +NoBlood
   }

   int m_dodgeDbc;
   int m_overdrive;

   override void beginPlay() {
      super.beginPlay();
      m_discrim = "Fulk";
      m_pclass = pcl_informant;
   }

   override void giveDefaultInventory()
   {
      super.giveDefaultInventory();
      lith_giveDefWeapon("Lith_Flintlock");
   }

   override void tick()
   {
      if(health > 0)
      {
         if(m_dodgeDbc)
            m_dodgeDbc--;
         else if(player && player.buttons & BT_SPEED)
         {
            let a = angle - atan2(player.cmd.sidemove, player.cmd.forwardmove);
            let c = cos(a) * 32;
            let s = sin(a) * 32;
            callACS("Lith_DodgeView");
            A_ChangeVelocity(c, s);
            m_iFrames = 20;
            m_dodgeDbc = TICRATE;
         }
      }

      super.tick();
   }
}

class Lith_IWeapon : Lith_Weapon
{
   default
   {
      Inventory.RestrictedTo "Lith_InformantPlayer";
   }
}

#include "lzscript/3_Informant/2_Flintlock.zsc"

/* EOF */
