// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Alison G. Watson. Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_Revolver : Lith_MWeapon;

default
{
   Tag "$LITH_INFO_SHORT_Revolver";
   Weapon.SlotNumber 2;
   Weapon.SlotPriority 0.9;
   Weapon.BobSpeed 2.5;
   Weapon.BobRangeX 0.2;
   Weapon.BobRangeY 0.3;
   Weapon.UpSound "weapons/revolver/draw";
   Lith_Weapon.MagSize 6;
   Lith_Weapon.AmmoType "Lith_BulletAmmo";
   Inventory.AltHudIcon "REVLA0";

   +WEAPON.NOAUTOFIRE
}

override void markPrecacheSounds() {
   super.markPrecacheSounds();
   markSound("weapons/revolver/fire");
   markSound("weapons/revolver/reload");
   markSound("weapons/ionrifle/empty");
}
action(Weapon) void Lith_A_FireRevolver()
{
   lith_takeAmmo(2);
   Lith_A_MagIncr();
   lith_gunFlash();
   soundAlert(invoker.owner);
   Lith_FireBullets(0, 0, -1, 45);
   Lith_A_GunSmokeAndSparksSmall();
   Lith_A_RecoilUp(0.75);
   A_StartSound("weapons/revolver/fire", lch_weapon);
   A_FireProjectile("Lith_LightProjectileRedShort", 0);
}

states(Weapon)
{
Ready:
   REVO A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   REVO A 1 Lith_A_Lower(_raise_fast);
   loop;
Select:
   REVO A 1 Lith_A_Raise(_raise_fast);
   loop;

Fire:
   REVO A 0 Lith_JumpNoAmmo(2);
   REVO A 0 Lith_A_JumpIfMagEmpty;
   REVO B 1 bright Lith_A_FireRevolver;
   REVO C 1 bright Lith_A_RecoilDn(0.25);
   REVO D 1 bright Lith_A_RecoilDn(0.25);
   REVO E 2 Lith_A_RecoilDn(0.25);
   REVO FD 2;
   REVO AAA 1 A_WeaponReady(WRF_ALLOWRELOAD);
   goto ReadySelector;

FireEmpty:
   #### # 1;
   #### # 1 offset(0, 45);
   #### # 1 offset(0, 43);
   #### # 1 offset(0, 40) A_StartSound("weapons/ionrifle/empty", lch_dryweapon);
   #### # 1 offset(0, 38);
   #### # 1 offset(0, 36);
   #### # 1 offset(0, 33);
   #### # 1 offset(0, 32);
   #### # 2;
   goto ReadySelector;

Reload:
   #### # 0 Lith_A_JumpIfMagFull;
   REVO A 1 offset(0, 33) {
      A_StartSound("weapons/revolver/reload", lch_reload);
      lith_a_setInterpolation(true);
   }
   REVO A 1 offset(0, 34);
   REVO A 1 offset(0, 35) Lith_A_ReloadBobDn(0.1);
   REVO A 1 offset(-1, 37) Lith_A_ReloadBobDn(0.1);
   REVO A 1 offset(-4, 40) Lith_A_ReloadBobDn(0.1);
   REVO G 1 offset(-7, 42) Lith_A_ReloadBobDn(0.1);
   REVO G 1 offset(-9, 48) Lith_A_ReloadBobDn(0.1);
   REVO H 1 offset(-11, 50) {
      Lith_A_ReloadBobDn(0.1);
      for(int i = 0; i < invoker.m_Fired; i++)
         Lith_A_Casing("Lith_CasSmall", 10, frandom(-1,1), 44, frandom(1,2), frandom(1,3), 0);
   }
   REVO H 1 offset(-12, 55) Lith_A_ReloadBobDn(0.1);
   REVO I 1 offset(-13, 64);
   REVO I 1 offset(-13, 69) lith_a_setInterpolation(false);
   TNT1 A 16;
   TNT1 A 2 Lith_A_ReloadBobUp(0.5);
   TNT1 A 2 Lith_A_ReloadBobUp(0.5);
   REVO J 1 offset(2, 64) {
      Lith_A_ReloadFinish();
      Lith_A_ReloadBobUp(0.3);
   }
   REVO J 1 offset(2, 55) {
      Lith_A_ReloadBobUp(0.2);
      lith_a_setInterpolation(true);
   }
   REVO K 1 offset(2, 50);
   REVO K 1 offset(2, 45);
   REVO K 1 offset(1, 40);
   REVO A 1 offset(1, 35);
   REVO A 1 offset(1, 33);
   REVO A 1 offset(1, 32) lith_a_setInterpolation(false);
   goto ReadySelector;
}

/* EOF */
