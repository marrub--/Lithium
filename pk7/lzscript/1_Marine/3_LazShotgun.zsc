// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_LazShotgunTrail : Actor {
   default {
      RenderStyle "Add";
      Scale 0.34;
      +NOINTERACTION
   }

   override void postBeginPlay() {
      super.postBeginPlay();
      if(random(0, 255) < 10) {
         SetState(ResolveState("Spawn2"));
      }
   }

   override void tick() {
      if(!isFrozen()) {
         a_fadeOut(0.1);
      }
   }

   states
   {
   Spawn:
      BLLT E -1 bright;
      stop;
   Spawn2:
      BLLT E -1 bright light("Lith_FinalizerLight");
      stop;
   }
}

class Lith_LazShotgunPuff : Actor
{
   default
   {
      DamageType "Electric";

      +ALWAYSPUFF
      +FORCEDECAL
   }
}

class Lith_LazShotgun : Lith_MWeapon;

default
{
   Tag "$LITH_INFO_SHORT_LazShotgun";
   Weapon.SlotNumber 3;
   Weapon.SlotPriority 0.9;
   Weapon.UpSound "weapons/lshotgun/draw";
   Inventory.AltHudIcon "LSHTZ0";

   +Weapon.NoAutoFire
   +Lith_Weapon.HitScan
}

action(Weapon) void Lith_A_LaserFire()
{
   SoundAlert(Invoker.Owner);
   class<Actor> trail = "Lith_LazShotgunTrail";
   if(lith_weapons_fastlazshot) {
      trail = "";
   }
   for(int i = 0; i < 7; i++)
      A_RailAttack(7 * Random(1, 4), 0, true, "", "#eb002b", RGF_SILENT | RGF_FULLBRIGHT, 2, "Lith_LazShotgunPuff", 4, 4, 0, 10, 5, 0, trail, -8);
   A_StartSound("weapons/lshotgun/fire", lch_weapon);
   A_Recoil(0.5);
   Lith_A_RecoilUp(0.75);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

states(Weapon)
{
Ready:
   LSHT A 1 Lith_A_Ready(WRF_ALLOWRELOAD);
   loop;

Deselect:
   LSHT A 1 Lith_A_Lower(_raise_fast);
   loop;

Select:
   LSHT A 1 Lith_A_Raise(_raise_fast);
   loop;

Fire:
   LSHT I 2 bright Lith_A_LaserFire;
   LSHT H 1 bright;
   LSHT A 4;
   LSHT B 1 Lith_A_RecoilDn(0.375);
   #### # 0 lith_a_setInterpolation(true);
   LSHT B 1 Lith_A_RecoilDn(0.375);
   LSHT C 2 A_StartSound("weapons/lshotgun/open", lch_reload);
   #### # 0 lith_a_setInterpolation(false);
   LSHT D 2;
   #### # 0 lith_a_setInterpolation(true);
   LSHT E 3;
   #### # 0 lith_a_setInterpolation(false);
   LSHT F 5;
   #### # 0 lith_a_setInterpolation(true);
   LSHT G 2 A_StartSound("weapons/lshotgun/close", lch_reload2);
   #### # 0 lith_a_setInterpolation(false);
   LSHT E 2;
   #### # 0 lith_a_setInterpolation(true);
   LSHT D 2;
   #### # 0 lith_a_setInterpolation(false);
   LSHT C 3;
   #### # 0 lith_a_setInterpolation(true);
   LSHT B 2;
   #### # 0 lith_a_setInterpolation(false);
   LSHT A 2;
   LSHT AAAAAAA 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
