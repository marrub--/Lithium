// ╭──────────────────────────────────────────────────────────────────────────╮
// │                                                                          │
// │             Distributed under the CC0 public domain license.             │
// │   By Marrub / Alison.  Attribution is encouraged, though not required.   │
// │                See licenses/cc0.txt for more information.                │
// │                                                                          │
// ╰──────────────────────────────────────────────────────────────────────────╯

class Lith_OmegaRailPuff : Lith_BasicPuff {}

class Lith_BFG9000 : Lith_MWeapon;

default
{
   Lith_Weapon.SpawnType "Lith_BFG9000Pickup";
   Lith_Weapon.AdsInSound "weapons/sniper/zoomin";
   Lith_Weapon.AdsOutSound "weapons/sniper/zoomout";
   Lith_Weapon.AmmoType "Lith_CannonAmmo";
   Tag "$LITH_INFO_SHORT_BFG9000";
   Weapon.BobRangeX 1.7;
   Weapon.BobSpeed 0.7;
   Weapon.SlotNumber 7;
   Weapon.SlotPriority 1;
   Weapon.UpSound "weapons/cannon/draw";
   Inventory.AltHudIcon "BFUGA0";
   +Weapon.NoAutoFire
   -Lith_Weapon.HitScan
}

action(Weapon) void Lith_A_FirePunct()
{
   invoker.bHitScan = true;
   SoundAlert(Invoker.Owner);
   lith_takeAmmo(2);
   lith_gunFlash();
   soundAlert(invoker.owner);
   CallACS("Lith_PunctuatorFire", 0);
}

action(Weapon) void Lith_A_FireCannonBig()
{
   invoker.bHitScan = false;
   lith_gunFlash();
   soundAlert(invoker.owner);
   lith_takeAmmo(2);
   A_StartSound("weapons/cannon/firebig", lch_weapon);
   A_Recoil(4);
   Lith_A_RecoilUp(4);
   A_FireProjectile("Lith_CannonBall", 0, false, 0, -24, FPF_NOAUTOAIM);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

action(Weapon) void Lith_A_FireCannonSmall()
{
   invoker.bHitScan = false;
   lith_gunFlash();
   soundAlert(invoker.owner);
   lith_takeAmmo(1);
   A_StartSound("weapons/cannon/fire", lch_weapon);
   A_Recoil(2);
   Lith_A_RecoilUp(2);
   A_FireProjectile("Lith_SmallCannonBall", 0, false, 0, -16, FPF_NOAUTOAIM);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

action(Weapon) void Lith_A_FireDarkMatter()
{
   invoker.bHitScan = false;
   A_FireProjectile("Lith_DarkMatter", 0, false, 0, 0, FPF_NOAUTOAIM);
   lith_gunFlash();
   soundAlert(invoker.owner);
   A_StartSound("weapons/cannon/fired", lch_weapon);
}

action(Weapon) void Lith_A_FireOmegaRail()
{
   invoker.bHitScan = true;
   lith_takeAmmo(2);
   lith_gunFlash();
   soundAlert(invoker.owner);
   A_RailAttack(500, 0, false, "", "", RGF_SILENT, 0, "Lith_OmegaRailPuff", 0, 0, 0, 0, 0.9, 0, "Lith_OmegaRailBeam", -19);
   A_FireProjectile("Lith_OmegaRailHax", 0, false, 0, -19);
   A_FireProjectile("Lith_LightProjectileRedLong", 0);
}

override bool lith_adsShouldHappen() {
   return Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_A);
}

states(Weapon)
{
AltReady:
   SCOP A 1 Lith_A_Ready(WRF_NOBOB);
   loop;
Ready:
   DCNG A 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_C), 2);
   CNNG A 0;
   #### A 1 Lith_A_Ready;
   loop;

Deselect:
   DCNG A 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_C), 2);
   CNNG A 0;
   #### A 1 Lith_A_Lower(_raise_slow);
   loop;

Select:
   DCNG A 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_C), 2);
   CNNG A 0;
   #### A 1 Lith_A_Raise(_raise_slow);
   loop;

Fire:
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_A), "FirePunct");
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_B), "FireRail");
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_C), "FireDark");
   #### # 0 lith_jumpNoAmmo;
   CNNG C 1 bright offset(0, 48);
   CNNG C 1 bright offset(0, 57) Lith_A_FireCannonSmall;
   CNNG C 1 bright offset(0, 55);
   CNNG C 1 bright offset(0, 53) Lith_A_RecoilDn(0.25);
   CNNG C 1 bright offset(0, 49) Lith_A_RecoilDn(0.25);
   CNNG B 1 bright offset(0, 45) Lith_A_RecoilDn(0.25);
   CNNG B 1 bright offset(0, 40) Lith_A_RecoilDn(0.25);
   CNNG B 1 bright offset(0, 37) Lith_A_RecoilDn(0.25);
   CNNG A 1 bright offset(0, 34) Lith_A_RecoilDn(0.25);
   CNNG A 1 offset(0, 32) Lith_A_RecoilDn(0.25);
   CNNG A 8 Lith_A_RecoilDn(0.25);
   CNNG AAAAAAAAAA 1 A_ReFire;
   goto ReadySelector;
FireDark:
   DCNG A 10 A_StartSound("weapons/cannon/prepare", lch_weapon2);
   DCNG A 1 bright;
   DCNG A 1 bright offset(-1, 33);
   DCNG A 1 bright offset(-1, 34);
   DCNG A 1 bright offset(-1, 36);
   DCNG A 2 bright offset(1, 37);
   DCNG A 3 bright offset(1, 38);
   DCNG B 1 bright offset(0, 62) Lith_A_FireDarkMatter;
   DCNG C 1 bright;
   DCNG D 1 bright offset(0, 57);
   DCNG E 1 bright offset(0, 47);
   DCNG F 1 bright offset(0, 44);
   DCNG G 1 bright offset(0, 40);
   DCNG H 1 bright offset(0, 37);
   DCNG I 1 bright offset(0, 35);
   DCNG J 1 bright offset(0, 32);
   DCNG KKKKLLLLLLLAAAAAAAAA 1 offset(0, 32) A_ReFire;
   goto ReadySelector;
FireRail:
   #### # 0 lith_jumpNoAmmo(2);
   CNNG A 5 offset(-1, 32) A_StartSound("weapons/rail/charge", lch_weapon2);
   CNNG A 5 offset(0, 32);
   CNNG A 5 offset(1, 32);
   CNNG A 5 offset(0, 33);
   CNNG A 5 offset(-1, 33);
   CNNG A 5 offset(0, 33);
   CNNG A 5 offset(1, 33);
   CNNG A 5 offset(0, 33);
FireRailLoop:
   #### # 0 lith_jumpNoAmmo(2, "ReadySelector");
   CNNG A 1 offset(0, 34);
   CNNG A 1 offset(0, 35);
   CNNG A 1 bright offset(0, 45);
   CNNG B 1 bright offset(0, 55) A_StartSound("weapons/rail/fire", lch_weapon);
   CNNG B 1 bright offset(0, 56);
   CNNG C 1 bright offset(0, 57) Lith_A_FireOmegaRail;
   CNNG C 1 bright offset(0, 47);
   CNNG A 1 offset(0, 42);
   CNNG A 2 offset(0, 37);
   CNNG A 3 offset(0, 35);
   CNNG A 4 offset(0, 33);
   CNNG AAAAAA 1 offset(0, 32) A_ReFire("FireRailLoop");
   goto ReadySelector;
FirePunct:
   #### # 0 lith_jumpNoAmmo(2);
   #### # 0 A_StartSound("weapons/punct/fire", lch_weapon);
   #### # 0 A_JumpIf(invoker.m_ads, "FirePunct_Scoped");
   CNNG A 3;
   CNNG A 1 bright offset(0, 36);
   CNNG B 1 bright offset(0, 38);
   CNNG B 1 bright offset(0, 49);
   CNNG C 1 bright offset(0, 52) Lith_A_FirePunct;
   CNNG C 1 bright offset(0, 37);
   CNNG A 1 bright offset(0, 36);
   CNNG A 2 bright offset(0, 35);
   CNNG AAAAAAAAAA 1 offset(0, 34) A_ReFire;
   goto LoadAnim;
FirePunct_Scoped:
   SCOP A 3;
   SCOP A 1 offset(0, 36) lith_a_adsZoomFactor(0.80);
   SCOP A 1 offset(0, 38) lith_a_adsZoomFactor(0.85);
   SCOP A 1 bright offset(0, 49) lith_a_adsZoomFactor(0.90);
   SCOP A 1 bright offset(0, 52) {
      lith_a_adsZoomFactor(0.95);
      Lith_A_FirePunct();
   }
   SCOP A 1 bright offset(0, 37) lith_a_adsZoomFactor(1.00);
   SCOP A 1 bright offset(0, 36);
   SCOP A 2 bright offset(0, 35);
   SCOP AAAAAAAAAA 1 offset(0, 34) A_ReFire;
LoadAnim:
   #### A 1 offset(1, 32);
   #### # 0 lith_a_setInterpolation(true);
   #### A 1 offset(3, 32);
   #### A 1 offset(4, 32);
   #### A 1 offset(8, 38);
   #### A 1 offset(12, 45);
   #### A 1 offset(16, 53);
   #### A 1 offset(19, 60);
   #### A 1 offset(21, 64);
   #### A 1 offset(23, 67);
   #### A 1 offset(22, 69) A_StartSound("weapons/punct/load", lch_reload);
   #### A 1 offset(19, 70);
   #### A 7 offset(18, 72);
   #### A 1 offset(17, 69);
   #### A 1 offset(19, 67);
   #### A 1 offset(20, 66);
   #### A 1 offset(21, 62);
   #### A 1 offset(20, 60);
   #### A 1 offset(21, 63);
   #### A 1 offset(23, 66);
   #### A 1 offset(24, 68);
   #### A 1 offset(25, 63);
   #### A 1 offset(24, 59);
   #### A 1 offset(22, 57);
   #### A 1 offset(20, 54);
   #### A 1 offset(16, 49);
   #### A 1 offset(12, 44);
   #### A 1 offset(10, 42);
   #### A 1 offset(8, 40);
   #### A 1 offset(5, 38);
   #### A 1 offset(2, 35);
   #### # 0 lith_a_setInterpolation(false);
   goto ReadySelector;

AltFire:
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_B), "Ready");
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_C), "Ready");
   #### # 0 A_JumpIf(Lith_UTIL.pData(_pdt_upgrade, UPGR_Cannon_A), "ScopeToggle");
   #### # 0 lith_jumpNoAmmo(2);
   CNNG A 10 A_StartSound("weapons/cannon/prepare", lch_weapon2);
   CNNG A 1 bright;
   #### # 0 lith_a_setInterpolation(true);
   CNNG A 1 bright offset(-1, 33);
   CNNG A 1 bright offset(-1, 34);
   CNNG A 1 bright offset(-1, 36);
   CNNG A 2 bright offset(1, 37);
   CNNG A 3 bright offset(1, 38);
   #### # 0 lith_a_setInterpolation(false);
   CNNG C 2 bright offset(0, 72) Lith_A_FireCannonBig;
   CNNG C 1 bright offset(0, 67) Lith_A_RecoilDn(0.5);
   CNNG C 1 bright offset(0, 57) Lith_A_RecoilDn(0.5);
   CNNG C 1 bright offset(0, 44) Lith_A_RecoilDn(0.5);
   CNNG B 1 bright offset(0, 40) Lith_A_RecoilDn(0.5);
   CNNG B 1 bright offset(0, 37) Lith_A_RecoilDn(0.5);
   CNNG B 1 bright offset(0, 35) Lith_A_RecoilDn(0.5);
   CNNG B 1 bright offset(0, 32) Lith_A_RecoilDn(0.5);
   CNNG A 1 bright Lith_A_RecoilDn(0.5);
   CNNG AAAAAAAAAAAAAAAAAAA 1 offset(0, 32) A_ReFire;
   goto ReadySelector;
ScopeToggle:
   #### A 1;
   #### # 0 Lith_A_AdsToggle(_ads_scope);
   goto ReadySelector;

FireEmpty:
   #### # 2 A_StartSound("weapons/cannon/empty", lch_dryweapon);
   #### # 1 offset(0, 28);
   #### # 1 offset(0, 29);
   #### # 1 offset(0, 32);
   #### # 1 offset(0, 35);
   #### # 1 offset(0, 40);
   #### # 1 offset(0, 42);
   #### # 1 offset(0, 43);
   #### # 1 offset(0, 44);
   #### # 1 offset(0, 40);
   #### # 1 offset(0, 37);
   #### # 1 offset(0, 34);
   #### # 1 offset(0, 32);
   #### # 1 A_ReFire;
   goto ReadySelector;
}

/* EOF */
