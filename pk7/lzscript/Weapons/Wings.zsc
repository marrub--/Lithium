/* ---------------------------------------------------------------------------|
 *
 * Distributed under the CC0 public domain license.
 * By Alison Sanderson. Attribution is encouraged, though not required.
 * See licenses/cc0.txt for more information.
 *
 * ---------------------------------------------------------------------------|
 */

class Lith_HeavenlyImage : Actor
{
   default
   {
      Alpha 0.8;
      RenderStyle "AddStencil";
      StencilColor "White";

      +NOINTERACTION
   }

   states
   {
   Spawn:
      #### # 3 light("Lith_HeavenlyImage");
      #### # 1 light("Lith_HeavenlyImage") A_FadeOut;
      wait;
   }
}

class Lith_IReallyHateTheseNoisyWings : Lith_CustomFunction
{
   states
   {
   Pickup:
      TNT1 A 0 {
         Thing_Hate(0, ACS_ExecuteWithResult(lsc_wdata, wdata_ptid), 0);
         A_Die();
      }
      stop;
   }
}

class Lith_WingsOfDeath : Lith_Weapon;

default
{
   Tag "Wings of Death";
   Weapon.SlotNumber 0;

   +WEAPON.CHEATNOTWEAPON
}

states(Weapon)
{
Ready:
   TNT1 A 1 A_WeaponReady(WRF_NOFIRE);
   TNT1 A 0 {
      A_SetBlend("FF FF FF", 0.1, 5);
      A_SpawnItemEx("Lith_HeavenlyImage", 0,-4,0, -5,-7,1, 0, SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
      A_SpawnItemEx("Lith_HeavenlyImage", 0, 4,0, -5, 7,1, 0, SXF_TRANSFERSPRITEFRAME|SXF_NOCHECKPOSITION);
      A_GiveInventory("PowerFlight");
      A_AlertMonsters();
      A_RadiusGive("Lith_IReallyHateTheseNoisyWings", 1024, RGF_MONSTERS);
   }
   TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 40 == 0, "Flap");
   TNT1 A 0 A_JumpIf(CallACS("Lith_Timer") % 2  == 0, "Ready");
   TNT1 A 0 A_ChangeVelocity(0, 0, -0.2);
   loop;
Flap:
   TNT1 A 0 {
      A_ChangeVelocity(0, 0, 4);
      A_StartSound("player/wings", lch_weapon);
   }
   goto ReadySelector;

Deselect:
   TNT1 A 0 A_TakeInventory("PowerFlight");
   TNT1 A 0 A_Lower;
   wait;
Select:
   TNT1 A 0 A_Raise;
   wait;

Fire:
   goto ReadySelector;
}

/* EOF */
